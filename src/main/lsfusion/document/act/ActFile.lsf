MODULE ActFile;

REQUIRE Act, DocFile;

NAMESPACE Act;

file (Act c) = GROUP LAST file(DocumentFile f) ORDER dateTime(f), f IF c == document(f);

loadFile 'Загрузить' (Act c) { 
    INPUT f = FILE DO NEW cf = DocumentFile {
        document(cf) <- c;
        file(cf) <- f;
        dateTime(cf) <- currentDateTime();
        user(cf) <- currentUser();
    } 
}
openFile 'Открыть' (Act c) { open(file(c)); }

file(Act c) += file(c);

EXTEND FORM act 
    OBJECTS f = DocumentFile
    PROPERTIES(f) READONLY date, time, nameUser, loginUser
    PROPERTIES(f) GRID open, DELETE 
    ORDER date(f), time(f)
    FILTERS document(f) == o
    
    PROPERTIES (o) loadFile, openFile SHOWIF TRUE IF file(o)
;

DESIGN act {
    tab {
        NEW files {
            type = CONTAINERV;
            caption = 'Файл';
            NEW file {
                caption = 'Текущий файл документа';
                type = CONTAINERH;
                MOVE PROPERTY (loadFile(o));
                MOVE PROPERTY (openFile(o));
            }
            MOVE BOX (f);
        }
    }
}

EXTEND FORM contract PROPERTIES DRAW ac TOOLBAR openFile(ac);