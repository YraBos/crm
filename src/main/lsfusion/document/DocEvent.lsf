MODULE DocEvent;

REQUIRE ContractEvent, AgreementEvent, ActEvent, DocDashboard;

NAMESPACE Doc;

addEventDoc 'Добавить' (Document d) {
    NEWSESSION NEW e = Event {
        contract(e) <- d IF d IS Contract;
        agreement(e) <- d IF d IS Agreement;
        act(e) <- d IF d IS Act;
        SHOW event OBJECTS o = e DOCKED;
    }
} IMAGE 'add.png';

WHEN LOCAL SETCHANGED (contract(Event e)) AND NOT CHANGED (customer(e)) AND NOT CHANGED (legalEntity(e)) DO {
    customer(e) <- customer(contract(e));
    legalEntity(e) <- legalEntity(contract(e));
}
WHEN LOCAL SETCHANGED (agreement(Event e)) AND NOT CHANGED (contract(e)) DO {
    contract(e) <- contract(agreement(e));
}
WHEN LOCAL SETCHANGED (act(Event e)) AND NOT CHANGED (contract(e)) DO {
    contract(e) <- contract(act(e));
    agreement(e) <- agreement(act(e));
}

CONSTRAINT contract(Event e) AND agreement(e) AND NOT contract(agreement(e)) == contract(e) CHECKED BY agreement[Event] MESSAGE 'Соглашение не соответствует договору';
CONSTRAINT contract(Event e) AND act(e) AND NOT contract(act(e)) == contract(e) CHECKED BY act[Event] MESSAGE 'Акт не соответствует договору';
CONSTRAINT agreement(Event e) AND act(e) AND NOT agreement(act(e)) == agreement(e) CHECKED BY act[Event] MESSAGE 'Акт не соответствует соглашению';
CONSTRAINT contract(Event e) AND legalEntity(e) AND NOT legalEntity(e) == legalEntity(contract(e)) MESSAGE 'Договор не соответствует организации';
CONSTRAINT agreement(Event e) AND legalEntity(e) AND NOT legalEntity(e) == legalEntity(agreement(e)) MESSAGE 'Соглашение не соответствует организации';
CONSTRAINT act(Event e) AND legalEntity(e) AND NOT legalEntity(e) == legalEntity(act(e)) MESSAGE 'Акт не соответствует организации';
CONSTRAINT contact(Event e) AND legalEntity(e) AND NOT legalEntity(e) == legalEntity(contact(e)) CHECKED BY contact[Event] MESSAGE 'Контактное лицо не соответствует организации';

EXTEND FORM docDashboard 
    OBJECTS e = Event
    PROPERTIES (e) READONLY id, name, nameCustomer, nameLegalEntity, nameEmployee, nameContact, date, time, dateNext, descriptionString, additionString
    PROPERTIES  addEventDoc(d) DRAW e TOOLBAR 
    PROPERTIES (e) NEWSESSION EDIT, DELETE 
    FILTERS contract(e) == d OR agreement(e) == d OR act(e) == d
;