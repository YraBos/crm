MODULE Doc;

REQUIRE Customer, LegalEntity;

NAMESPACE Doc;

CLASS ABSTRACT Document 'Документ';

id 'Код' = ABSTRACT VARSTRING[15] (Document) IN id CHARWIDTH 5;
number 'Номер' = ABSTRACT VARSTRING[15](Document) IN id CHARWIDTH 10;
date 'Дата' = ABSTRACT DATE (Document) IN id;
name 'Наименование' = ABSTRACT VARISTRING[150](Document) IN id CHARWIDTH 20;
nameType 'Тип' = ABSTRACT VARISTRING[100](Document) CHARWIDTH 15;
dateSigningCompany 'Дата подписания компанией' = ABSTRACT DATE (Document);
dateSending 'Дата отправки' = ABSTRACT DATE (Document);
dateSigningCustomer 'Дата подписания клиентом' = ABSTRACT DATE (Document);
dateReceiving 'Дата получения' = ABSTRACT DATE (Document);
dateClosing 'Дата закрытия' = ABSTRACT DATE (Document);
note 'Примечание' = ABSTRACT VARISTRING[250](Document);
file 'Файл' = ABSTRACT FILE (Document);
sum 'Сумма' = ABSTRACT NUMERIC[16,5](Document);
paid 'Оплачено' = ABSTRACT NUMERIC[16,5](Document);
balance 'Остаток' (Document d) = sum(d) (-) paid(d);
dateLastPaid 'Дата последней оплаты' = ABSTRACT DATE (Document);

customer = ABSTRACT Customer (Document);
nameCustomer 'Клиент' (Document d) = name(customer(d));

legalEntity = ABSTRACT LegalEntity(Document);
nameLegalEntity 'Организация' (Document d) = name(legalEntity(d)); 

company = ABSTRACT LegalEntity(Document);
nameCompany 'Компания' (Document d) = name(company(d)); 

CLASS DocType 'Вид договора';
name 'Наименование' (DocType d) = staticCaption(d) IF d IS DocType CHARWIDTH 20;

FORM dialogDocTypes 'Типы документа'
    OBJECTS d = DocType
    PROPERTIES READONLY name(d)
    
    LIST DocType OBJECT d
;

docType = ABSTRACT DocType(Document);
nameDocType 'Вид договора' (Document d) = name(docType(d));

WHEN LOCAL SET (Document d IS Document) AND NOT date(d) DO {
    date(d) <- currentDate();    
}