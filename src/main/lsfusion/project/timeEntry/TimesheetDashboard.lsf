MODULE TimesheetDashboard;

REQUIRE TimeEntryTotalDashboard, TimeEntryAbsense;

NAMESPACE Project;

absenseHours 'Часы' (Employee e, AbsenceReason r, INTEGER y) = GROUP SUM hours(Absence a) (-) workedHours(a) BY employee(a), reason(a), extractYear(date(a));

addTimeEntry 'Добавить' (DATE d) {
    NEWSESSION NEW t = TimeEntry {
        date(t) <- d;
        SHOW timeEntry OBJECTS o = t DOCKED;
    }
} IMAGE 'add.png' CHANGEKEY 'INSERT' HIDE;

@addRoleApplicationSetting(allowEditOtherTimesheets, 'Разрешить редактировать чужие табели рабочего времени', BOOLEAN);

FORM timesheetDashboard 'Табель рабочего времени'
    
    OBJECTS m = Month PANEL, y = INTEGER PANEL, e = Employee PANEL
    PROPERTIES month 'Месяц' = staticCaption(m) SELECTOR, year 'Год' = VALUE (y), name(e) SELECTOR READONLYIF NOT allowEditOtherTimesheets(mainRole(currentUser()))
    FILTERS e == currentUser() OR allowEditOtherTimesheets(mainRole(currentUser()))
    
    OBJECTS d = DATE 
    PROPERTIES READONLY VALUE (d), extractDOWName(d), workingHours(d), totalHours(e, d)
//    FILTERS workingHours(d) OR totalHours(e, d)
    FILTERS iterate(d, toDateFormat('01' + lpad(TEXT (number(m)), 2, '0') + y, 'DDMMYYYY'),
                       lastDayOfMonth(toDateFormat('01' + lpad(TEXT (number(m)), 2, '0') + y, 'DDMMYYYY')))
                                    
    
    OBJECTS t = TimeEntry
    PROPERTIES (t) READONLY id, hours, commentsString, date, nameActivity, nameProject, nameCustomer, numberIssue, nameIssue, descriptionAbsense
    PROPERTIES DRAW t TOOLBAR changeAbsense(t), addTimeEntry(d)
    PROPERTIES (t) SHOWIF NOT disableEditing(t) NEWSESSION EDIT, DELETE 
    ORDER date(t)
    FILTERS employee(t) == e
    FILTERGROUP tday FILTER 'За день' date(t) == d DEFAULT 
                     FILTER 'За месяц' extractMonth(date(t)) == m AND extractYear(date(t)) == y
                     FILTER 'За год' extractYear(date(t)) == y
    
    OBJECTS a = Absence
    PROPERTIES (a) READONLY id, nameEmployee, nameReason, dateFrom, dateTo, hours, workedHours, workingHours, date, note
    PROPERTIES (a) NEWSESSION NEW, EDIT SHOWIF isEditable(a), DELETE SHOWIF isEditable(a)
    ORDER date(a)
    FILTERS employee(a) == e
    FILTERGROUP aday FILTER 'За день' date(a) == d DEFAULT
                     FILTER 'За месяц' extractMonth(date(a)) == m AND extractYear(date(a)) == y
                     FILTER 'За год' extractYear(date(a)) == y
    
    OBJECTS r = AbsenceReason
    PROPERTIES READONLY name(r), absenseHours(e, r, y)
    FILTERS absenseHours(e, r, y)
    
    EVENTS ON INIT {
        SEEK timesheetDashboard.m = extractMonth(currentDate());
        SEEK timesheetDashboard.y = extractYear(currentDate());
        SEEK timesheetDashboard.e = currentUser();
    }
;

DESIGN timesheetDashboard {
    OBJECTS {
        NEW header FIRST {
            caption = 'Фильтры';
            type = CONTAINERH;
            MOVE PROPERTY (month) { charWidth = 15; }
            MOVE PROPERTY (year) { charWidth = 5; }
            MOVE PROPERTY (name(e)) { charWidth = 25; }
        }
        NEW bottom {
            type = SPLITH;
            fill = 1;
            NEW left {
                MOVE BOX (d) { 
                    fill = 2;
                    PROPERTY (workingHours(d)) { charWidth = 10; }
                    PROPERTY (totalHours(e, d)) { charWidth = 10; }
                }
                MOVE BOX (r) { 
                    caption = 'Отсутствие за год'; 
                    fill = 1;
                }
                fill = 1;
            }
            NEW right {
                type = CONTAINERV;
                MOVE BOX (t) { fill = 2; }
                MOVE BOX (a) { fill = 1; }
                fill = 3;
            }
        }
    }
}

NAVIGATOR {
    dashboard {
        NEW timesheetDashboard;
    }
}