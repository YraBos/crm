MODULE Contact;

REQUIRE LegalEntity, Authentication;

NAMESPACE Contact;

CLASS Contact 'Контакт' : Authentication.CustomUser;
TABLE contact (Contact);

id 'Код' = DATA VARSTRING[15] (Contact) INDEXED CHARWIDTH 5 IN id;
contact (id) = GROUP AGGR Contact c BY id (c);

legalEntity = DATA LegalEntity(Contact);
idLegalEntity 'Код организации' (Contact c) = id(legalEntity(c));
nameLegalEntity 'Организация' (Contact c) = name(legalEntity(c)) IN id;

customer = DATA Customer(Contact);
idCustomer 'Код клиента' (Contact c) = id(customer(c));
nameCustomer 'Клиент' (Contact c) = name(customer(c)) IN id;

merge (Customer from, Customer to) + { customer(Contact c) <- to WHERE customer(c) = from; }

middleName 'Отчество' = DATA VARSTRING[100](Contact) CHARWIDTH 15;
initials 'Инициалы' (Contact c) = VARSTRING[5](CONCAT '', left(firstName(c), 1) + '. ', left(lastName(c), 1) + '.') CHARWIDTH 5;
shortName 'ФИО (сокр.)' (Contact c) = CONCAT ' ', lastName(c), initials(c) CHARWIDTH 20;
fullName 'ФИО (полное)' (Contact c) = CONCAT ' ', lastName(c), firstName(c), middleName(c) CHARWIDTH 30;

position 'Должность' = DATA VARSTRING[100](Contact) CHARWIDTH 30;
skype 'Skype' = DATA VARSTRING[50](Contact) CHARWIDTH 15;
note 'Примечание' = DATA VARSTRING[100](Contact) CHARWIDTH 20;

CLASS Phone 'Телефон';
TABLE phone(Phone);

number 'Номер телефона' = DATA VARSTRING[20](Phone);

CLASS PhoneType 'Тип телефона';
TABLE phoneType (PhoneType);

name 'Наименование' = DATA VARSTRING[50](PhoneType);

type = DATA PhoneType(Phone);
nameType 'Тип телефона' (Phone p) = name(type(p));

FORM phoneType 'Тип телефона'
    OBJECTS t = PhoneType PANEL 
    PROPERTIES (t) name 
    
    EDIT PhoneType OBJECT t
;

FORM phoneTypes 'Типы телефона'
    OBJECTS t = PhoneType
    PROPERTIES (t) READONLY name
    PROPERTIES (t) NEWSESSION NEW, EDIT, DELETE 
    
    LIST PhoneType OBJECT t
;

contact (Phone p) = DATA Contact(Phone) NONULL DELETE;

addPhones 'Доп. телефоны' (Contact c) = GROUP CONCAT number(Phone p), ', ' BY contact(p) CHARWIDTH 15;

skipCustomer 'Клиент не обязателен' = ABSTRACT BOOLEAN (Contact);

CONSTRAINT Contact c IS Contact AND NOT customer(c) AND NOT skipCustomer(c) MESSAGE 'Для контакта должен быть задан клиент';
CONSTRAINT customer(Contact c) != customer(legalEntity(c)) CHECKED BY legalEntity[Contact] MESSAGE 'Клиент не совпадает с организацией';

WHEN LOCAL SETCHANGED (customer(legalEntity(Contact c))) DO {
    customer(c) <- customer(legalEntity(c));
}

FORM contact 'Контакт'
    OBJECTS o = Contact PANEL
    PROPERTIES(o) id, lastName, firstName, middleName, nameCustomer, nameLegalEntity, position, email, phone, skype, note
    
    OBJECTS p = Phone
    PROPERTIES (p) nameType, number, NEW, DELETE 
    FILTERS contact(p) == o
    
    EDIT Contact OBJECT o
;

DESIGN contact {
    GROUP (,o) {
        type = CONTAINERV;
    }
}

FORM contacts 'Контакты'
    OBJECTS o = Contact
    PROPERTIES(o) READONLY id, lastName, firstName, middleName, nameCustomer, nameLegalEntity, position, email, phone, addPhones, skype, note
    PROPERTIES(o) NEWSESSION NEW, EDIT, DELETE
    ORDER lastName(o)
;

FORM dialogContacts 'Контакты'
    OBJECTS o = Contact
    PROPERTIES(o) READONLY id, lastName, firstName, middleName, nameCustomer, nameLegalEntity, position, email, phone, addPhones, skype, note
    ORDER lastName(o)
    
    LIST Contact OBJECT o
;

NAVIGATOR {
    customerFolder {
        NEW contacts;
    }
}

authorisedPerson = DATA Contact(LegalEntity);
nameAuthorisedPerson 'Уполномоченное лицо' (LegalEntity l) = name(authorisedPerson(l));

CONSTRAINT authorisedPerson(LegalEntity l) AND NOT legalEntity(authorisedPerson(l)) == l CHECKED BY authorisedPerson[LegalEntity] 
    MESSAGE 'Организация уполномоченного лица не соответствует организации';

EXTEND FORM legalEntities PROPERTIES nameAuthorisedPerson(o);
EXTEND FORM dialogLegalEntities PROPERTIES nameAuthorisedPerson(o);
EXTEND FORM legalEntity PROPERTIES nameAuthorisedPerson(o);

DESIGN legalEntity {
    row {
        type = CONTAINERV;
        client {
            type = CONTAINERH;
            caption = 'Контакты';
            MOVE PROPERTY (nameAuthorisedPerson(o)) { charWidth = 30; }
        }
    }
}