MODULE ContractFile;

REQUIRE Contract, DocFile;

NAMESPACE Contract;

file (Contract c) = GROUP LAST file(DocumentFile f) ORDER dateTime(f), f IF c == document(f);
loadFile 'Загрузить' (Contract c) { 
    INPUT f = FILE DO NEW cf = DocumentFile {
        document(cf) <- c;
        file(cf) <- f;
        dateTime(cf) <- currentDateTime();
        user(cf) <- currentUser();
    } 
}
loadFileApply 'Загрузить' (Contract c) {
    NEWSESSION {
        loadFile(c);
        APPLY;
    }
}

openFile 'Открыть' (Contract c) { open(file(c)); }

file(Contract c) += file(c);

EXTEND FORM contract 
    OBJECTS f = DocumentFile
    PROPERTIES(f) READONLY date, time, nameUser, loginUser
    PROPERTIES(f) GRID open, DELETE 
    ORDER date(f), time(f)
    FILTERS document(f) == o
    
    PROPERTIES (o) loadFile, openFile SHOWIF TRUE IF file(o)
;

DESIGN contract {
    details {
        NEW files AFTER text {
            type = CONTAINERV;
            caption = 'Файл';
            NEW file {
                caption = 'Текущий файл документа';
                type = CONTAINERH;
                MOVE PROPERTY (loadFile(o));
                MOVE PROPERTY (openFile(o));
            }
            MOVE BOX (f);
        }
    }
}