MODULE PaymentAct;

REQUIRE PaymentAgreement, Act;

act = DATA Act(Payment);
idAct 'Код акта' (Payment a) = id(act(a));
numberAct 'Номер акта' (Payment a) = number(act(a)) IN id;

paidPayment 'Оплачено' (Act c) = GROUP SUM sum(Payment p) BY act(p);

paidContract 'Оплачено' (Act c) = PARTITION UNGROUP paid
                                                      LIMIT sum(c) (-) paidPayment(c)
                                                      ORDER date(c),c 
                                                      BY contract(c); 
                                                      
paid 'Оплачено' (Act a) = paidContract(a) (+) paidPayment(a);

paid(Act c) += paid(c);                          

CONSTRAINT contract(act(Payment p)) != contract(p) CHECKED BY act[Payment] MESSAGE 'Договор акта должен совпадать с договором платежа';

split 'Разбить платеж' (Payment p) {
    DIALOG dialogInputSum OBJECTS n INPUT DO {
        IF n < sum(p) THEN NEW pp = Payment {
            sum(p) <- sum(p) (-) n;
            sum(pp) <- n;
            legalEntity(pp) <- legalEntity(p);
            company(pp) <- company(p);
            contract(pp) <- contract(p);
            act(pp) <- act(p);
            number(pp) <- number(p);
            date(pp) <- date(p);
            note(pp) <- note(p);
            currency(pp) <- currency(p);
        }
        ELSE MESSAGE 'Сумма больше суммы платежа';
    }
}

EXTEND FORM payment PROPERTIES (o) numberAct;

DESIGN payment {
    docs {
        MOVE PROPERTY (numberAct(o));
    }
}

EXTEND FORM payments 
    PROPERTIES (o) READONLY numberAct
;
EXTEND FORM dialogPayments 
    PROPERTIES (o) READONLY numberAct
;

EXTEND FORM contract PROPERTIES READONLY paid(ac);