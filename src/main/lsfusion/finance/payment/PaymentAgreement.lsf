MODULE PaymentAgreement;

REQUIRE PaymentContract, Agreement;

NAMESPACE Payment;

agreement = DATA Agreement(Payment);
numberAgreement 'Номер соглашения' (Payment p) = number(agreement(p));

paidPayment 'Оплачено' (Agreement a) = GROUP SUM sum(Payment p) BY agreement(p);

paidContract 'Оплачено' (Agreement a) = PARTITION UNGROUP paid
                                                      LIMIT sum(a) (-) paidPayment(a)
                                                      ORDER date(a),a 
                                                      BY contract(a); 
                                                      
paid 'Оплачено' (Agreement a) = paidContract(a) (+) paidPayment(a);

paid(Agreement c) += paid(c);             

CONSTRAINT contract(agreement(Payment p)) != contract(p) CHECKED BY agreement[Payment] MESSAGE 'Договор соглашения должен совпадать с договором платежа';

EXTEND FORM payment PROPERTIES (o) numberAgreement;

DESIGN payment {
    docs {
        MOVE PROPERTY (numberAgreement(o));
    }
}

EXTEND FORM payments 
    PROPERTIES (o) READONLY numberAgreement
;
EXTEND FORM dialogPayments 
    PROPERTIES (o) READONLY numberAgreement
;

EXTEND FORM contract PROPERTIES READONLY paid(ag);
