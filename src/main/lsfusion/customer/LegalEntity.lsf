MODULE LegalEntity;

REQUIRE Customer, Currency, Address;

NAMESPACE LegalEntity;

CLASS LegalEntity 'Организация';
TABLE legalEntity (LegalEntity);

id 'Код' = DATA VARSTRING[15] (LegalEntity) INDEXED CHARWIDTH 5 IN id;
legalEntity (id) = GROUP AGGR LegalEntity le BY id (le);
isCompany 'Компания' = DATA BOOLEAN (LegalEntity);

extId 'Код для экспорта' = DATA VARSTRING[15](LegalEntity) IN id;
legalEntityEx (id) = GROUP AGGR LegalEntity le BY extId (le);

name 'Наименование' = DATA VARISTRING[250] (LegalEntity) CHARWIDTH 20 IN id;

legalEntity = DATA LegalEntity(Address);
nameLegalEntity 'Организация' (Address a) = name(legalEntity(a));
physicalAddress (LegalEntity l) = GROUP LAST Address a ORDER a IF in(a, AddressType.physical) BY legalEntity(a);
namePhysicalAddress 'Физ. адрес' (LegalEntity l) = name(physicalAddress(l));
legalAddress (LegalEntity l) = GROUP LAST Address a ORDER a IF in(a, AddressType.legal) BY legalEntity(a);
nameLegalAddress 'Юр. адрес' (LegalEntity l) = name(legalAddress(l));

customer = DATA Customer(LegalEntity);
idCustomer 'Код клиента' (LegalEntity l) = id(customer(l));
nameCustomer 'Клиент' (LegalEntity l) = name(customer(l)) IN id;

merge (Customer from, Customer to) + { customer(LegalEntity l) <- to WHERE customer(l) = from; }

CLASS Bank 'Банк';
TABLE bank (Bank);

id 'Код' = DATA VARSTRING[15] (Bank) MATERIALIZED INDEXED CHARWIDTH 5 IN id;
bank (id) = GROUP AGGR Bank b BY id (b);

number 'Код банка' = DATA VARSTRING[10](Bank) CHARWIDTH 5 IN id;
name 'Наименование' = DATA VARISTRING[100] (Bank) CHARWIDTH 15 IN id;
postcode 'Почтовый индекс' = DATA VARSTRING[50](Bank) CHARWIDTH 6;
address 'Адрес' = DATA VARSTRING[250](Bank) CHARWIDTH 20;
mfo 'МФО' = DATA VARSTRING[11] (Bank);

FORM bank 'Банк'
    OBJECTS o = Bank PANEL
    PROPERTIES(o) id, number, name, address, mfo
    
    EDIT Bank OBJECT o
;

FORM banks 'Банки'
    OBJECTS o = Bank
    PROPERTIES(o) READONLYIF isReadonly() id, number, name, address, mfo
    PROPERTIES(o) NEWSESSION NEW, EDIT, DELETE
    ORDER name(o)
;

@extendFormEditable(banks);

FORM dialogBanks 'Банки'
    OBJECTS o = Bank
    PROPERTIES(o) READONLY id, number, name, address, mfo
    ORDER name(o)
    
    LIST Bank OBJECT o
;

NAVIGATOR {
    customerFolder {
        NEW banks;
    }
}

CLASS Account 'Расчетный счет';
TABLE account (Account);

inactive 'Неактивная' = DATA BOOLEAN (Account);
active 'Активная' (Account a) = a IS Account AND NOT inactive(a);

number 'Номер расчетного счета'  = DATA VARSTRING[50] (Account) IN id CHARWIDTH 28 FIXED;

account (number) = GROUP MAX Account account BY number(account);

bank = DATA Bank (Account);
nameBank 'Наименование банка' (Account account) = name(bank(account)) IN id;
addressBank 'Адрес банка' (Account account) = address(bank(account));
MFOBank 'Код BIC банка' (Account account) = mfo(bank(account));

legalEntity = DATA LegalEntity (Account) NONULL DELETE AUTOSET;
nameLegalEntity 'Организация' (Account account) = name(legalEntity(account));

account (number, legalEntity) = GROUP MAX Account account BY number(account), legalEntity(account);

defaultAccount(legalEntity) = GROUP MAX Account account IF active(account) BY legalEntity(account);

userAccount = DATA Account (LegalEntity);
CONSTRAINT legalEntity(userAccount(LegalEntity legalEntity)) != legalEntity CHECKED MESSAGE 'ошибка: Р/сч. по умолчанию должен соответствовать р/сч. Ю.Л.';

account (LegalEntity legalEntity) =  OVERRIDE userAccount(legalEntity), defaultAccount(legalEntity) MATERIALIZED INDEXED;
numberAccount 'Основной р/сч.' (LegalEntity legalEntity) = number(account(legalEntity));
nameBank 'Банк' (LegalEntity legalEntity) = nameBank(account(legalEntity));
addressBank 'Адрес банка' (LegalEntity legalEntity) = addressBank(account(legalEntity));
mfoBank 'Код BIC банка' (LegalEntity legalEntity) = MFOBank(account(legalEntity));
numberBank 'Код банка' (LegalEntity l) = number(bank(account(l)));
postcodeBank 'Почтовый индекс банка' (LegalEntity l) = postcode(bank(account(l)));

equals 'Основной' (LegalEntity legalEntity, Account account) = userAccount(legalEntity) == account;

accountID 'Расчетный счет по номеру' (number, legalEntityId) = GROUP AGGR Account account WHERE account IS Account BY number(account), id(legalEntity(account));
legalEntityAccount (VARSTRING[20] number) = legalEntity(account(number)); 

currency = DATA Currency (Account);
nameCurrency 'Валюта счета' = name(currency(Account account));

note 'Примечание'  = DATA VARISTRING[50] (Account);

EXTEND FORM customer PROPERTIES nameLegalEntity(a) AFTER types(a);

WHEN CHANGED (legalEntity(Address a)) AND NOT customer(a) DO {
    customer(a) <- customer(legalEntity(a));
}

CONSTRAINT legalEntity(Address a) AND customer(a) AND NOT customer(legalEntity(a)) == customer(a) CHECKED BY legalEntity[Address], customer[Address] 
    MESSAGE 'Организация адреса не соответствует организации клиента адреса';

FORM legalEntity 'Организация'
    OBJECTS o = LegalEntity PANEL
    PROPERTIES(o) id, extId, name, nameCustomer, nameBank, numberAccount, isCompany
    ORDER name(o)
    
    OBJECTS a=Account
    PROPERTIES(a)  inactive, number, nameCurrency, nameBank, addressBank, MFOBank, note, NEW, DELETE GRID
    FILTERS legalEntity(a) == o
    PROPERTIES equals(o, a)
    
    OBJECTS d = Address
    PROPERTIES (d) types ON CHANGE changeAddressTypes(d), postcode, region, district, locality, street, houseNumber, officeNumber, NEW, DELETE 
    ORDER types(d) DESC 
    FILTERS legalEntity(d) == o
    
    FILTERGROUP inactive FILTER 'Активный' active(a) 'ctrl F10' DEFAULT 
    
    EDIT LegalEntity OBJECT o
;

DESIGN legalEntity {
    OBJECTS {
        NEW main FIRST {
            type = CONTAINERH;
            caption = 'Основные параметры';
            MOVE PROPERTY (id(o));
            MOVE PROPERTY (name(o));
            MOVE PROPERTY (isCompany(o));
            MOVE PROPERTY (extId(o));
        }
        NEW row AFTER main {
            type = CONTAINERH;
            NEW client {
                caption = 'Клиент';
                MOVE PROPERTY (nameCustomer(o)) {charWidth = 30; }
            }
            NEW bank {
                caption = 'Реквизиты';
                type = CONTAINERH;
                MOVE PROPERTY (numberAccount(o));
                MOVE PROPERTY (nameBank(o)) { charWidth = 30; }
            }
        }
        NEW tab {
            fill = 1;
            type = TABBED;
            MOVE BOX (a);
            MOVE BOX (d);
        }
    }
}

FORM legalEntities 'Организации'
    OBJECTS o = LegalEntity
    PROPERTIES(o) READONLYIF isReadonly() id, extId, name, nameCustomer, nameLegalAddress, numberBank, 
                             nameBank, addressBank, mfoBank, numberAccount, isCompany
    PROPERTIES(o) NEWSESSION NEW, EDIT, DELETE
    ORDER name(o)
;

@extendFormEditable(legalEntities);

FORM dialogLegalEntities 'Организации'
    OBJECTS o = LegalEntity
    PROPERTIES(o) READONLY id, extId, name, nameCustomer, nameLegalAddress, numberBank, nameBank, addressBank, mfoBank, numberAccount, isCompany
    ORDER name(o)
    
    LIST LegalEntity OBJECT o
;

NAVIGATOR {
    customerFolder {
        NEW legalEntities;
    }
}

skipReceipt 'Не учитывать в доходах' = DATA BOOLEAN (LegalEntity);

EXTEND FORM legalEntity PROPERTIES skipReceipt(o);
DESIGN legalEntity {
    main {
        MOVE PROPERTY (skipReceipt(o));
    }   
}