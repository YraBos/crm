MODULE EventDashboard;

REQUIRE Event, Dashboard;

NAMESPACE Event;

lastEvent (Customer c, Employee ee) = GROUP LAST Event e ORDER date(e), time(e), e BY customer(e), employee(e);
lastEvent (Customer c) = GROUP LAST Event e ORDER date(e), time(e), e BY customer(e);
isLastCustomerEmployee (Event e) = e = lastEvent(customer(e), employee(e));
isLastCustomer (Event e) = e = lastEvent(customer(e));

FORM eventDashboard 'Обработка событий'
    OBJECTS o = Event 
    PROPERTIES(o) READONLY id, name, nameType, nameCustomer, nameLegalEntity, nameEmployee, nameContact, date, time, dateNext, descriptionString, additionString
    PROPERTIES(o) PANEL READONLY description, addition
    PROPERTIES(o) NEWSESSION NEW, EDIT, DELETE
    ORDER date(o), time(o)
    
    FILTERGROUP last FILTER 'Последние по клиенту (мои)' isLastCustomerEmployee(o) AND employee(o) = currentUser() AND dateNext(o) >= subtract(currentDate(), 360) 
                     FILTER 'Последние по клиенту' isLastCustomer(o) AND dateNext(o) >= subtract(currentDate(), 360)
;

DESIGN eventDashboard {
    OBJECTS {
        BOX (o) {
            fill = 5;
        }
        type = SPLITV;
        NEW bottom AFTER BOX (o){
            type = CONTAINERH;
            fill = 1;
            NEW description {
                fill = 1;
                caption = 'Содержание';
                MOVE PROPERTY (description(o)) { caption = ''; fill = 1; }
            }
            NEW addition {
                fill = 1;
                caption = 'Дополнение';
                MOVE PROPERTY (addition(o)) { caption = ''; fill = 1; }
            }
        }
    }
}

NAVIGATOR {
    dashboard {
        NEW eventDashboard;
    }
}