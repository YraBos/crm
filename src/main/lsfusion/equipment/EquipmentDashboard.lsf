MODULE EquipmentDashboard;

REQUIRE Equipment, Dashboard, Utils;

NAMESPACE Equipment;

equipmentID 'Идентификатор оборудования' = DATA VARISTRING[20](SubjectType);

EXTEND FORM subjectType PROPERTIES equipmentID(o);
EXTEND FORM subjectTypes PROPERTIES READONLY equipmentID(o);

file = DATA FILE (Equipment);
downloadFile 'Открыть файл лицензии' (Equipment a)  { open(file(a), 'lic'); }

generateLicense = FORMULA TEXT 'generateLicense($1,$2)';

generateLicense 'Сгенерировать лицензию' (Equipment e) {
    stringToFile((GROUP CONCAT generateLicense(equipmentID(subjectType(e)), number(EquipmentSerial s)) IF equipment(s) == e, '\r\r\n'), 'UTF-8', '');
    file(e) <- resultFile();
}

EXTEND FORM equipment PROPERTIES generateLicense(o), downloadFile(o) SHOWIF TRUE IF file(o);

DESIGN equipment {
    GROUP (,o) {
        columns = 3;
    }
}

filterContract = DATA LOCAL Contract(SubjectType);
nameFilterContract 'Договор' (SubjectType t) = description(filterContract(t));

CONSTRAINT filterContract(SubjectType t) AND NOT in(filterContract(t), t) CHECKED BY filterContract[SubjectType] 
    MESSAGE 'В выбранном договоре нет предметов договора с выбранным типом';

addEquipment 'Добавить' (SubjectType t, Customer c) {
    NEWSESSION NESTED (filterContract[SubjectType]) NEW e = Equipment {
        subjectType(e) <- t;
        contract(e) <- filterContract(t);
        customer(e) <- c;
        SHOW equipment OBJECTS o = e DOCKED;
    }
} IMAGE 'add.png';

quantitySerial 'Кол-во серийных номеров' (Customer c, SubjectType t) = 
    GROUP SUM 1 IF contract(equipment(EquipmentSerial s)) == filterContract(t) OR NOT filterContract(t) BY customer(equipment(s));

FORM equipmentDashboard 'Оборудование'
    OBJECTS t = SubjectType PANEL 
    OBJECTS c = Customer PANEL 
    PROPERTIES name(t) SELECTOR, nameFilterContract(t), name(c) SELECTOR, quantitySerial(c, t) READONLY 
    FILTERS customer(filterContract(t)) == c OR NOT filterContract(t)
    
    OBJECTS e = Equipment
    PROPERTIES (e) READONLY note, quantitySerial, nameCreatedUser, createdDate, createdTime
    PROPERTIES addEquipment(t, c) DRAW e TOOLBAR 
    PROPERTIES (e) NEWSESSION EDIT, DELETE 
    FILTERS subjectType(e) == t, customer(e) == c, contract(e) == filterContract(t) OR NOT filterContract(t)
    
    OBJECTS n = EquipmentSerial
    PROPERTIES (n) READONLY number
    FILTERS equipment(n) == e
;

DESIGN equipmentDashboard {
    OBJECTS {
        NEW head FIRST {
            caption = 'Параметры';
            type = CONTAINERH;
            MOVE PROPERTY (name(t)) { caption = 'Тип предмета договора'; }
            MOVE PROPERTY (nameFilterContract(t)) { caption = 'Договор'; }
            MOVE PROPERTY (name(c)) { caption = 'Клиент'; }
            MOVE PROPERTY (quantitySerial(c, t));
        }
        NEW equip {
            type = CONTAINERH;
            fill = 1;
            MOVE BOX (e);
            MOVE BOX (n);
        }
    }
}

NAVIGATOR {
    dashboard {
        NEW equipmentDashboard;
    }
}