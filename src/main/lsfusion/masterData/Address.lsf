MODULE Address;

NAMESPACE Address;

CLASS Address 'Адрес';
TABLE address(Address);

CLASS AddressType 'Тип адреса' {
    physical 'Физический',
    legal 'Юридический'
}
name 'Наименование' (AddressType t) = staticCaption(t) IF t IS AddressType;

in 'Вкл.' = DATA BOOLEAN (Address, AddressType);
types 'Тип' (Address a) = GROUP CONCAT name(AddressType t) IF in(a, t), ', ' ORDER DESC name(t) CHARWIDTH 15;


FORM dialogAddressTypes 'Типы адреса'
    OBJECTS a = Address PANEL 
    OBJECTS t = AddressType
    PROPERTIES in(a, t), name(t) READONLY 
;

changeAddressTypes (Address a) {
    SHOW dialogAddressTypes;
}

postcode 'Индекс' = DATA VARISTRING[10] (Address) CHARWIDTH 6;
region 'Область' = DATA VARISTRING[20] (Address) CHARWIDTH 15;
district 'Район' = DATA VARISTRING[20] (Address) CHARWIDTH 15;
locality 'Нас. пункт' = DATA VARISTRING[50] (Address) CHARWIDTH 40 IN id;
street 'Улица' = DATA VARISTRING[50] (Address) CHARWIDTH 20;
houseNumber 'Дом' = DATA VARISTRING[20] (Address) CHARWIDTH 6;
officeNumber 'Офис' = DATA VARISTRING[20] (Address) CHARWIDTH 6;
extra 'Дополнительно' = DATA VARISTRING[100](Address) CHARWIDTH 40;
name 'Адрес' (Address a) = CONCAT ', ', locality(a), street(a), 'дом ' + houseNumber(a), 'офис ' + officeNumber(a), extra(a);

FORM addresses 'Адреса'
    OBJECTS a = Address
    PROPERTIES (a) READONLY types, postcode, region, district, locality, street, houseNumber, officeNumber, extra, name
    
    LIST Address OBJECT a
;