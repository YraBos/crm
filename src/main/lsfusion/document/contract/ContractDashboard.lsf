MODULE ContractDashboard;

REQUIRE PaymentAct, Dashboard, ContractFile, AgreementFile, ActFile, Debt;

NAMESPACE Contract;

close 'Закрыть' (Contract c) {
    NEWSESSION {
        dateClosing(c) <- currentDate();
        APPLY;
    }
} TOOLBAR;

filterContractType = DATA LOCAL ContractType();
nameFilterContractType 'Тип договора' () = name(filterContractType());

FORM contractDashboard 'Работа с договорами'
    PROPERTIES nameFilterContractType()
    OBJECTS o = Contract LAST 
    PROPERTIES(o) READONLY id, nameCustomer, nameLegalEntity, nameCompany, number, date, name, sum, shortNameCurrency, sumCurrency, paid, nameType, nameGroup, nameSendingWay, 
                           dateSigningCompany, dateSending, dateSigningCustomer, dateReceiving, dateTo, dateClosing, note, textString, text PANEL
    PROPERTIES(o) NEWSESSION NEW, EDIT, DELETE
    PROPERTIES(o) loadFileApply, openFile SHOWIF TRUE IF file(o), close SHOWIF NOT dateClosing(o)
    ORDER date(o)
    FILTERS type(o) == filterContractType() OR NOT filterContractType()
    
    FILTERGROUP paid FILTER 'Не оплачен' paid(o) < sum(o)
                     FILTER 'Оплачен' paid(o) >= sum(o)
    
    FILTERGROUP activeContract
        FILTER 'Активные' NOT dateClosing(o) < currentDate() DEFAULT
    
    OBJECTS ag = Agreement LAST 
    PROPERTIES (ag) READONLY id, numberContract, number, date, name, nameType, sum, paid, balance, dateClosing, dateSending, dateReceiving, note, textString
    PROPERTIES (ag) openFile DRAW ag TOOLBAR 
    ORDER date(ag)
    FILTERS contract(ag) == o
    
    OBJECTS ac = Act LAST 
    PROPERTIES (ac) READONLY id, numberContract, numberAgreement, number, date, name, nameType, sum, paid, balance, dateSending, dateReceiving, dateClosing, note, textString
    PROPERTIES (ac) openFile DRAW ac TOOLBAR 
    ORDER date(ac)
    FILTERS contract(ac) == o
    
    OBJECTS p = Payment LAST 
    PROPERTIES(p) READONLY id, nameLegalEntity, nameCustomer, numberContract, nameTypeContract, numberAct, number, date, 
                           year, monthNumber, sum, shortNameCurrency, sumCurrency, note
    ORDER date(p)
    FILTERS contract(p) == o

    OBJECTS d = Debt
    PROPERTIES (d) READONLY date, sum, shortNameCurrency, sumCurrency, paid
    ORDER date(o)
    FILTERS contract(d) == o
;

DESIGN contractDashboard {
    OBJECTS {
        NEW filter FIRST {
            caption = 'Фильтры';
            MOVE PROPERTY (nameFilterContractType());
        }
        NEW contract AFTER filter {
            MOVE BOX (o);
            type = SPLITV;
            fill = 1;
            NEW tab AFTER BOX (o) {
                type = TABBED;
                fill = 1;
                NEW text {
                    caption = 'Текст';
                    type = CONTAINERV;
                    MOVE PROPERTY (text(o)) {
                        fill = 1;
                        caption = ''; 
                    }
                    NEW file {
                        type = CONTAINERH;
                        caption = 'Файл документа';
                        MOVE PROPERTY (loadFileApply(o));
                        MOVE PROPERTY (openFile(o));
                    }
                }
                MOVE BOX (ag);
                MOVE BOX (ac);
                MOVE BOX (p) {
                    caption = 'Платежи';
                }
                NEW debt {
                    caption = 'Задолженность';
                    MOVE BOX (d);
                }
            }
        }
    }
}
NAVIGATOR {
    dashboard {
        NEW contractDashboard;
    }
}