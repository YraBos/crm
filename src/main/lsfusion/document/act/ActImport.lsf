MODULE ActImport;

REQUIRE Act, ContractImport;

NAMESPACE Act;

importActs 'Импорт актов выполненных работ' (FILE f) {
    LOCAL idAct = STRING[15](INTEGER);
    LOCAL idContract = STRING[15](INTEGER);
    LOCAL idAgreement = STRING[15](INTEGER);
    LOCAL idPersonLegalEntity = STRING[15](INTEGER);
    LOCAL docPersonLegalEntity = STRING[50](INTEGER);
    LOCAL idPersonCompany = STRING[15](INTEGER);
    LOCAL docPersonCompany = STRING[50](INTEGER);
    LOCAL number = STRING[15](INTEGER);
    LOCAL date1 = DATE (INTEGER);
    LOCAL name = STRING[150](INTEGER);
    LOCAL type = STRING[50](INTEGER);
    LOCAL nameSending = STRING[50](INTEGER);
    LOCAL sum = NUMERIC[16,5] (INTEGER);
    LOCAL sending = DATE (INTEGER);
    LOCAL receiving = DATE (INTEGER);
    LOCAL closing = DATE (INTEGER);
    LOCAL fromDate = DATE (INTEGER);
    LOCAL toDate = DATE (INTEGER);
    LOCAL note = STRING[100](INTEGER);
    LOCAL text = TEXT(INTEGER);
    LOCAL message = STRING[2000] ();
    LOCAL count = INTEGER ();
    
    message() <- '';
    count() <- 0;
        
    IMPORT XLS NOHEADER FROM f TO idAct = B, idContract, idAgreement, idPersonCompany, docPersonCompany, idPersonLegalEntity, 
        docPersonLegalEntity, number, date1, name, type, sum, nameSending, sending, receiving, closing, fromDate, toDate, note, text;
    
    FOR imported(INTEGER i) AND i > 1 AND trim(idContract(i)) AND NOT contract(trim(idContract(i))) DO {
    
        message() <- CONCAT ' ', message() + '\r\nстрока:', (i + 1), 'id:', idAct(i), 'Код договора:', idContract(i);
        count() <- count() + 1;
    }
    
    IF count() > 0 THEN {
        MESSAGE CONCAT ' ', 'Неимпортированные акты, для которых не найдены договоры: ', count(), message();
    }
    
    FOR [GROUP SUM 1 IF INTEGER i > 1 AND contract(trim(idContract(i))) BY trim(idAct(i))](STRING[15] id) AND id != '' AND NOT act(id) DO NEW c = Act {
        id(c) <- id;
    }
    
    FOR [GROUP SUM 1 IF INTEGER i > 1 BY trim(type(i))](STRING[50] type) AND type != '' 
        AND NOT (GROUP MAX ActType t IF upper(name(t)) == upper(type)) DO NEW t = ActType {
        
        name(t) <- type; 
    }
    
    FOR [GROUP SUM 1 IF INTEGER i > 1 BY trim(nameSending(i))](STRING[50] sending) AND sending != '' 
        AND NOT (GROUP MAX SendingWay t IF upper(name(t)) == upper(sending)) DO NEW t = SendingWay {
        
        name(t) <- sending; 
    }
    
    FOR Act a = act(trim(idAct(INTEGER i))) AND contract(trim(idContract(i))) AND trim(idAct(i)) != '' AND i > 1 DO {
        contract(a) <- contract(trim(idContract(i)));
        agreement(a) <- agreement(idAgreement(i));
        number(a) <- number(i);
        date(a) <- date1(i);
        name(a) <- trim(name(i));
        sum(a) <- sum(i);
        type(a) <- GROUP MAX ActType t IF upper(name(t)) == upper(trim(type(i)));
        sendingWay(a) <- GROUP MAX SendingWay s IF upper(name(s)) == upper(trim(nameSending(i)));
        dateSending(a) <- sending(i);
        dateReceiving(a) <- receiving(i);
        dateClosing(a) <- closing(i);
        note(a) <- trim(note(i));
        text(a) <- trim(text(i));
        fromDate(a) <- fromDate(i);
        toDate(a) <- toDate(i);
    }
}

importActs 'Импорт актов выполненных работ' () {
    NEWSESSION INPUT f = FILE DO {
        importActs(f);
        APPLY;
    }
}

FORM exportActs 'Экспорт актов'
    OBJECTS c = Act
    PROPERTIES (c) id, idContract, idAgreement, idAuthorisedPersonCompany, authorisedDocumentCompany, idAuthorisedPersonLegalEntity, 
                   authorisedDocumentLegalEntity, number, date, name, nameType, sum, nameSendingWay, dateSending, dateReceiving, 
                   dateClosing, fromDate, toDate, note, text
    ORDERS date(c)
;

exportActs 'Экспорт актов' () {
    
    LOCAL file = FILE();
    
    PRINT exportActs XLS TO file;
    WRITE CLIENT DIALOG file() TO 'Акты';
    open(file(), 'Акты');
}

EXTEND FORM migrationData PROPERTIES() exportActs, importActs;

importActDocumentsXls 'Импорт документов актов' () {
    
    NEWSESSION {
        LOCAL id = STRING[15](INTEGER);
        LOCAL path = STRING[250](INTEGER);
        LOCAL file = FILE();
        
        INPUT f = FILE DO {
            IMPORT XLS NOHEADER FROM f TO id, path;
            
            FOR Act c = act(id(INTEGER i)) AND id(i) AND i > 0 DO {
                TRY {
                    READ 'file://' + documentPath() + '/' + replace(upper(trim(path(i))), '\\', '/') TO file;
                    NEW df = DocumentFile {
                        document(df) <- c;
                        file(df) <- file();
                        dateTime(df) <- currentDateTime();
                        user(df) <- currentUser();
                    }
                }
            }
        }
        APPLY;
    }
}

EXTEND FORM migrationData PROPERTIES() importActDocumentsXls;
DESIGN migrationData { importDocuments { MOVE PROPERTY(importActDocumentsXls()); } }