MODULE ActImport;

REQUIRE Act, ContractImport;

NAMESPACE Act;

importActs 'Импорт актов выполненных работ' (FILE f) {
    LOCAL idAct = VARSTRING[15](INTEGER);
    LOCAL idContract = VARSTRING[15](INTEGER);
    LOCAL idAgreement = VARSTRING[15](INTEGER);
    LOCAL number = VARSTRING[15](INTEGER);
    LOCAL date1 = DATE (INTEGER);
    LOCAL name = VARSTRING[150](INTEGER);
    LOCAL type = VARSTRING[50](INTEGER);
    LOCAL sum = NUMERIC[16,5] (INTEGER);
    LOCAL sending = DATE (INTEGER);
    LOCAL receiving = DATE (INTEGER);
    LOCAL closing = DATE (INTEGER);
    LOCAL note = VARSTRING[100](INTEGER);
    LOCAL text = TEXT(INTEGER);
    LOCAL message = VARSTRING[2000] ();
    LOCAL count = INTEGER ();
    
    message() <- '';
    count() <- 0;
        
    IMPORT XLS NOHEADER FROM f TO idAct, idContract, idAgreement, number, date1, name, type, sum, sending, receiving, closing, note, text;
    
    FOR imported(INTEGER i) AND i > 2 AND trim(idContract(i)) AND NOT contract(trim(idContract(i))) DO {
    
        message() <- CONCAT ' ', message() + '\r\nстрока:', (i + 1), 'id:', idAct(i), 'Код договора:', idContract(i);
        count() <- count() + 1;
    }
    
    IF count() > 0 THEN {
        MESSAGE CONCAT ' ', 'Неимпортированные акты, для которых не найдены договоры: ', count(), message();
    }
    
    FOR [GROUP SUM 1 IF INTEGER i > 2 AND contract(trim(idContract(i))) BY trim(idAct(i))](VARSTRING[15] id) AND id != '' AND NOT act(id) DO NEW c = Act {
        id(c) <- id;
    }
    
    FOR [GROUP SUM 1 IF INTEGER i > 2 BY trim(type(i))](VARSTRING[50] type) AND type != '' 
        AND NOT (GROUP MAX ActType t IF upper(name(t)) == upper(type)) DO NEW t = ActType {
        
        name(t) <- type; 
    }
    
    FOR Act a = act(trim(idAct(INTEGER i))) AND contract(trim(idContract(i))) AND trim(idAct(i)) != '' AND i > 2 DO {
        contract(a) <- contract(trim(idContract(i)));
        agreement(a) <- agreement(idAgreement(i));
        number(a) <- number(i);
        date(a) <- date1(i);
        name(a) <- trim(name(i));
        sum(a) <- sum(i);
        type(a) <- GROUP MAX ActType t IF upper(name(t)) == upper(trim(type(i)));
        dateSending(a) <- sending(i);
        dateReceiving(a) <- receiving(i);
        dateClosing(a) <- closing(i);
        note(a) <- trim(note(i));
        text(a) <- trim(text(i));
    }
}

importActs 'Импорт актов выполненных работ' () {
    NEWSESSION INPUT f = FILE DO {
        importActs(f);
        APPLY;
    }
}

EXTEND FORM migrationData PROPERTIES() importActs;

importActDocumentsXls 'Импорт документов актов' () {
    
    NEWSESSION {
        LOCAL id = VARSTRING[15](INTEGER);
        LOCAL path = VARSTRING[250](INTEGER);
        LOCAL file = FILE();
        
        INPUT f = FILE DO {
            IMPORT XLS NOHEADER FROM f TO id, path;
            
            FOR Act c = act(id(INTEGER i)) AND id(i) AND i > 0 DO {
                TRY {
                    READ 'file://' + documentPath() + '/' + replace(upper(trim(path(i))), '\\', '/') TO file;
                    NEW df = DocumentFile {
                        document(df) <- c;
                        file(df) <- file();
                        dateTime(df) <- currentDateTime();
                        user(df) <- currentUser();
                    }
                }
            }
        }
        APPLY;
    }
}

EXTEND FORM migrationData PROPERTIES() importActDocumentsXls;
DESIGN migrationData { importDocuments { MOVE PROPERTY(importActDocumentsXls()); } }