MODULE Resource;

REQUIRE Contact;

CLASS Resource 'Ресурс';
TABLE resource (Resource);
name 'Наименование ресурса' = DATA ISTRING[100] (Resource);
responsiblePerson 'Ответственный' = DATA User(Resource);
note 'Примечание' = DATA TEXT (Resource) CHARWIDTH 50;
inUse 'Исп.' = DATA BOOLEAN (Resource);

///// Кем и когда создан
createdUser = DATA User(Resource);
nameCreatedUser 'Создан пользователем' (Resource s) = name(createdUser(s));

createdDate 'Дата создания' = DATA DATE (Resource);
createdTime 'Время создания' = DATA TIME (Resource);
createdDateTime 'Дата/время создания' (Resource r) = dateTimeToDateTime(createdDate(r), createdTime(r));
WHEN LOCAL SET(Resource r IS Resource) DO {
    createdUser(r) <- currentUser();
    createdDate(r) <- currentDate();
    createdTime(r) <- currentTime();
}
/////

CLASS Reservation 'Резервирование';
TABLE reservation (Reservation);

resourse 'Ресурс' = DATA Resource(Reservation) INDEXED NONULL DELETE AUTOSET ;
user 'Кто резервирует' = DATA User (Reservation);
nameUser 'Кто резервирует' = name(user(Reservation r));
 
dateFrom 'Дата с' = DATA DATE (Reservation) NONULL;
timeFrom 'Время с' = DATA TIME (Reservation) NONULL;
dateTimeFrom = dateTimeToDateTime(dateFrom(Reservation d), timeFrom(d));

dateTo 'Дата по' = DATA DATE (Reservation) NONULL;
timeTo 'Время по' = DATA TIME (Reservation) NONULL;
dateTimeTo = dateTimeToDateTime(dateTo(Reservation d), timeTo(d));

comments 'Комментарии' = DATA ISTRING[500] (Reservation);

///// Кем и когда создано
createdUser = DATA User(Reservation);
nameCreatedUser 'Создан пользователем' (Reservation s) = name(createdUser(s));

createdDate 'Дата создания' = DATA DATE (Reservation);
createdTime 'Время создания' = DATA TIME (Reservation);
createdDateTime 'Дата/время создания' (Reservation r) = dateTimeToDateTime(createdDate(r), createdTime(r));
WHEN LOCAL SET(Reservation r IS Reservation) DO {
    createdUser(r) <- currentUser();
    createdDate(r) <- currentDate();
    createdTime(r) <- currentTime();
}
/////

//dateTimeInResourse(Reservation r, DATETIME t) = IF   
 
CONSTRAINT dateTimeFrom(Reservation r) >= dateTimeTo(r) 
    MESSAGE 'Дата/время по должно быть больше, чем Дата/время с'; 

CONSTRAINT SETCHANGED(user(Reservation r)) AND NOT (name(customer(user(r))) = 'ЛюксСофт' OR name(customer(user(r))) = 'ЛюксСофт Сервис')
    CHECKED BY user[Reservation] MESSAGE 'Только ЛюксСофт'; 

FORM resource 'Ресурс'
    OBJECTS r = Resource PANEL  
    PROPERTIES(r) inUse, name, responsiblePerson, note
    
    EDIT Resource OBJECT r
;

FORM reservation 'Резервирование'
    OBJECTS d = Reservation PANEL
    PROPERTIES(d) dateFrom, timeFrom, dateTo, timeTo, nameUser, comments
    
    EDIT Reservation OBJECT d
;

FORM resources 'Ресурсы'
    OBJECTS r = Resource  
    PROPERTIES(r) READONLY inUse, name, responsiblePerson, note, nameCreatedUser, createdDateTime
    PROPERTIES(r) NEWSESSION NEW, EDIT, DELETE 
    FILTERGROUP inUse
        FILTER 'Используемые' inUse(r) DEFAULT    
    ORDERS name(r)    
    
    OBJECTS d = Reservation
    PROPERTIES(d) READONLY dateFrom, timeFrom, dateTo, timeTo, nameUser, comments, nameCreatedUser, createdDateTime
    PROPERTIES(d) NEWSESSION NEW, EDIT, DELETE
    ORDERS dateFrom(d), timeFrom(d) 
    FILTERS resourse(d) == r    
    FILTERGROUP actual
        FILTER 'Актуальные' dateTo(d)>=currentDate() DEFAULT
    
    LIST Resource OBJECT r
;

NAVIGATOR {
    NEW FOLDER resource 'Ресурсы' WINDOW toolbar  {
        NEW resources;
    }
}