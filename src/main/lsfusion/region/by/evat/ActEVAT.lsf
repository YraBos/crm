MODULE ActEVAT;

REQUIRE Act, EVAT;

NAMESPACE Act;

lastEVATAct 'Последний ЭСЧФ по акту' = GROUP LAST EVAT e ORDER date(e), e WHERE NOT cancelled(e) BY EVATDocument(e) MATERIALIZED ;

in 'Вкл.' = DATA LOCAL BOOLEAN (Act);

viewEVATActDateMore 'Отображать акты при формировании ЭСЧФ с датой не меньше ' = DATA DATE () ;

EXTEND FORM options PROPERTIES viewEVATActDateMore();
DESIGN options { commons { MOVE PROPERTY (viewEVATActDateMore()); } }

isEVAT 'Формировать ЭСЧФ' = DATA BOOLEAN (LegalEntity);
EXTEND FORM legalEntity PROPERTIES(o) isEVAT;
EXTEND FORM legalEntities PROPERTIES(o) READONLY isEVAT;

DESIGN legalEntity {
    bank{
        MOVE PROPERTY (UNP(o));
        MOVE PROPERTY (OKPO(o));
        MOVE PROPERTY (isEVAT(o));
    }
}

generateEVAT 'Сформировать ЭСЧФ' ()  {
    
    LOCAL count = INTEGER ();
    count() <- 0;
    
    FOR in(Act a) DO NEWSESSION NESTED LOCAL {
        NEW e = EVAT {
            EVATDocument(e) <- a;
            generateSeriesNumber(e);
            unpSender(e) <- '101262078';
            date(e) <- currentDate();
            customer(e) <- legalEntity(contract(a));
            supplier(e) <- company(contract(a));
            dataDateDoc(e) <- date(a);
            numberContract(e) <- numberContract(a);
            dateContract(e) <- dateContract(a);
            numberDoc(e) <- number(a);
            
            NEW d = EVATDetail {
                EVAT(d) <- e;
                number(d) <- 1; 
                quantity(d) <- 0;
                price(d) <- 0;
                sum(d) <- sum(a);
                exciseSum(d) <- 0;
                vatRate(d) <- 0;
                vatRateType(d) <- 'NO_VAT';
                vatSum(d) <- 0;
//                sumWithVAT(d) <- sum(d) + vatSum(d);
                sumWithVAT(d) <- sum(d);
                codeOced(d) <-'62010';
                code(d) <-'';
                name(d) <- 'По договору '+numberContract(a);
                descriptionType(d) <- DescriptionType.vatExcemption;
            }
        }
        in(a) <- NULL;
        count() <- count() + 1;
        APPLY NESTED LOCAL;
    }
    MESSAGE CONCAT ' ', 'Количество созданных ЭСЧФ: ', count() ;
}


FORM ActEVATs 'Формирование электронных счет-фактур'
    OBJECTS a = Act
    PROPERTIES(a) in
    PROPERTIES(a) READONLY nameCompany, number, date, dateSending, dateReceiving, name, nameType, numberContract, numberAgreement, nameLegalEntity, sum, textString SELECTOR
    PROPERTIES() generateEVAT DRAW a TOOLBAR
    FILTERS NOT lastEVATAct(a) AND isEVAT(company(a)) AND (NOT viewEVATActDateMore() OR date(a) >= viewEVATActDateMore())
    
    OBJECTS dates = (dFrom = DATE, dTo = DATE) PANEL
    PROPERTIES dFrom = VALUE(dFrom), dTo = VALUE(dTo)
    
    OBJECTS e = EVAT
    PROPERTIES(e) READONLYIF isReadonly() exported, cancelled, unpSender, number, exportYear, exportNumber, dateDoc, numberDoc, seriesDoc,
                  nameSupplier, nameCustomer, totalSum,
                  totalExciseSum, totalVATSum, totalSumWithVAT
    
    FILTERS date(e) >= dFrom, date(e) <= dTo
    
    FILTERGROUP exported FILTER 'Выгруженные' exported(e) 
                         FILTER 'Не выгруженные' noexported(e) 
    FILTERGROUP cancelled FILTER 'Отмененные' cancelled(e) 
                          FILTER 'Не отмененные' nocancelled(e) DEFAULT 
;

DESIGN ActEVATs {
    OBJECTS{
        NEW acts BEFORE BOX(e){
            type = TABBED;
            fill = 1;
            
            MOVE BOX(a) {
                MOVE PROPERTY(generateEVAT());
            }
        }
        NEW filters BEFORE BOX(e) {
            MOVE BOX(dates) {
                GROUP(,dates) {
                    type = CONTAINERH;
                    PROPERTY(dFrom) {
                        caption = 'Дата с';
                    }
                    PROPERTY(dTo) {
                        caption = 'Дата по';
                    }
                }
            }
        }
    }
}

NAVIGATOR {
    EVATFolder {
        NEW ActEVATs;
    }
}

