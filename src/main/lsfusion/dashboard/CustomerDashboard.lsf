MODULE CustomerDashboard;

REQUIRE Project, PaymentAct, Event, Issue, TimeEntry, Dashboard, LegalEntity, DocFile, TimeEntryCustomer, ProjectExternal, DebtAgreement, CustomerAttachment;

NAMESPACE Dashboard;

addEvent 'Добавить' (Customer c) {
    NEWSESSION NEW e = Event {
        customer(e) <- c;
        SHOW event OBJECTS o = e DOCKED;
    }
} IMAGE 'add.png'  CHANGEKEY 'INSERT' HIDE;

addContact 'Добавить' (Customer c) {
    NEWSESSION NEW e = Contact.Contact {
        customer(e) <- c;
        SHOW contact OBJECTS o = e DOCKED;
    }
} IMAGE 'add.png'  CHANGEKEY 'INSERT' HIDE;

addLegalEntity 'Добавить' (Customer c) {
    NEWSESSION NEW e = LegalEntity {
        customer(e) <- c;
        SHOW legalEntity OBJECTS o = e DOCKED;
    }
} IMAGE 'add.png'  CHANGEKEY 'INSERT' HIDE;

filterLegalEntity = DATA LOCAL LegalEntity(Customer);
nameFilterLegalEntity 'Организация' (Customer c) = name(filterLegalEntity(c));

CONSTRAINT filterLegalEntity(Customer c) AND NOT customer(filterLegalEntity(c)) == c CHECKED BY filterLegalEntity[Customer] MESSAGE 'Клиент организации не соответствует клиенту';

addIssue 'Добавить' (Customer c) {
    NEWSESSION NEW i = Issue {
        customer(i) <- c;
        SHOW issue OBJECTS o = i DOCKED;
    }
} IMAGE 'add.png'  CHANGEKEY 'INSERT' HIDE;

FORM dashboardCustomer 'Работа с клиентами'
    
    OBJECTS c = Customer PANEL 
    PROPERTIES (c) name SELECTOR, info
    
    OBJECTS le = LegalEntity
    PROPERTIES (le) READONLY id, name, fullName, nameLegalAddress, numberBank, nameBank, addressBank, mfoBank, isCompany
    PROPERTIES addLegalEntity(c) DRAW le TOOLBAR
    PROPERTIES (le) NEWSESSION EDIT, DELETE 
    FILTERS customer(le) == c
    ORDER name(le)
    
    OBJECTS ct = Contact.Contact
    PROPERTIES (ct) READONLY id, name, nameLegalEntity, namePosition, email, phone, addPhones, skype, note
    PROPERTIES addContact(c) DRAW ct TOOLBAR 
    PROPERTIES (ct) NEWSESSION EDIT, DELETE 
    FILTERS customer(ct) == c
    ORDER name(ct)
    
    OBJECTS cr = Contract
    PROPERTIES (cr) READONLY id, nameLegalEntity, nameCompany, number, date, name, sum, paid, nameType, nameSendingWay, dateSigningCompany, dateSending, dateSigningCustomer, dateReceiving, dateTo, dateClosing, note, textString
    PROPERTIES (cr) NEWSESSION EDIT 
    FILTERS customer(legalEntity(cr)) == c OR customer(company(cr)) == c
    ORDER date(cr) DESC
    
    FILTERGROUP activeContract
        FILTER 'Активные' NOT dateClosing(cr) <= currentDate() DEFAULT
    
    OBJECTS ag = Agreement
    PROPERTIES (ag) READONLY id, number, date, name, nameType, sum, dateSending, dateReceiving, dateClosing, note, textString
    PROPERTIES (ag) NEWSESSION EDIT 
    FILTERS contract(ag) == cr
    ORDER date(ag) DESC
    
    OBJECTS ac = Act
    PROPERTIES (ac) READONLY id, numberAgreement, number, date, name, paid, nameType, sum, dateSending, dateReceiving, dateClosing, note, textString
    PROPERTIES (ac) NEWSESSION EDIT 
    FILTERS contract(ac) == cr
    ORDER date(ac) DESC

    OBJECTS ppm = Payment
    PROPERTIES (ppm) READONLY id, numberAct, number, date, year, monthNumber, sum, nameCurrency, shortNameCurrency, sumCurrency, note 
    FILTERS contract(ppm) == cr
    ORDER date(ppm) DESC
    
    PROPERTIES nameFilterLegalEntity(c)
    
    OBJECTS pm = Payment
    PROPERTIES (pm) READONLY id, nameLegalEntity, numberContract, nameContract, nameTypeContract, numberAgreement, nameAgreement, 
                             numberAct, number, date, year, monthNumber, sum, nameCurrency, shortNameCurrency, sumCurrency, note 
    FILTERS customer(legalEntity(pm)) == c, legalEntity(pm) == filterLegalEntity(c) OR NOT filterLegalEntity(c)
    ORDER date(pm) DESC
    
    OBJECTS ev = Event
    PROPERTIES (ev) READONLY id, name, nameType, nameLegalEntity, nameEmployee, nameContact, date, time, dateNext, descriptionString, description PANEL, addition PANEL 
    PROPERTIES addEvent(c) DRAW ev TOOLBAR 
    PROPERTIES (ev) NEWSESSION EDIT, DELETE 
    FILTERS customer(ev) == c
    ORDER date(ev) DESC
    
    OBJECTS pr = Project
    PROPERTIES (pr) READONLY id, name, identifier
    FILTERS customer(pr) == c
    ORDER name(pr) DESC
    
    OBJECTS st = IssueStatus
    PROPERTIES (st) READONLY name
    PROPERTIES READONLY count(c, st)
    FILTERS count(c, st)
    
    OBJECTS is = Issue
    PROPERTIES(is) in
    PROPERTIES(is) READONLY number, name, descriptionString, dateTo, dateTimeCreated, dateTimeUpdated, dateFrom, doneRatio, estimatedHours, nameProject, nameAuthor, nameAssignedTo, nameTracker
    PROPERTIES(is) changeStatus TOOLBAR, open TOOLBAR
    FILTERS customer(is) == c OR customer(project(is)) == c, status(is) == st
    ORDER number(is) DESC
    
    OBJECTS te = TimeEntry
    PROPERTIES (te) READONLY id, hours, commentsString, date, nameActivity, nameProject, nameEmployee, numberIssue, nameIssue
    FILTERS customer(te) == c
    ORDER date(te) DESC
    
    OBJECTS ss = Issue
    PROPERTIES(ss) in
    PROPERTIES(ss) READONLY number, name, descriptionString, dateTo, dateTimeCreated, dateTimeUpdated, dateFrom, doneRatio, 
                            estimatedHours, nameProject, nameAuthor, nameAssignedTo, nameTracker
    PROPERTIES DRAW ss TOOLBAR open(ss), addIssue(c)
    PROPERTIES (ss) SHOWIF NOT external(ss) NEWSESSION EDIT, DELETE 
    FILTERS customer(ss) == c OR customer(project(ss)) == c
    FILTERGROUP open FILTER 'Открытые' NOT isClosed(status(ss)) DEFAULT 
    FILTERGROUP inner FILTER 'Внутренние' NOT external(ss) DEFAULT 
    ORDER number(ss) DESC
    
    OBJECTS cd = (df = DATE, dt = DATE) PANEL 
    PROPERTIES dFrom 'Дата с' = VALUE (df), dTo 'Дата по' =  VALUE (dt), printCustomerReport(c, df, dt)
    
    OBJECTS at = Attachment
    PROPERTIES (at) READONLY id, nameCustomer, number, date, nameType, note, nameCreatedUser, createdDate
    PROPERTIES DRAW at TOOLBAR openFile(at), addAttachment(c)
    PROPERTIES(at) NEWSESSION EDIT, DELETE
    ORDER date(at) DESC 
    FILTERS customer(at) == c 
    
    OBJECTS d = Debt
    PROPERTIES (d) READONLY date, numberContract, nameContractType, nameLegalEntity, shortNameCurrency, sumCurrency, sum, paid, note, numberAgreement
    FILTERS customer(d) = c
;

DESIGN dashboardCustomer {
    OBJECTS {
        NEW tabPane AFTER BOX (c) {
            type = TABBED;
            fill = 1;
            MOVE BOX (le) { caption = 'Организации'; }
            MOVE BOX (ct) { caption = 'Контакты'; }
            NEW contracts {
                caption = 'Договоры';
                type = SPLITV;
                MOVE BOX (cr) { caption = 'Договоры'; }
                NEW contractDetails {
                    fill = 1;
                    type = TABBED;
                    NEW additionalContractDocuments {
                        caption = 'Документы';
                        type = SPLITH;
                        MOVE BOX (ag) { caption = 'Соглашения'; }
                        MOVE BOX (ac) { caption = 'Акты'; }
                    }
                    MOVE BOX(ppm) {
                        caption = 'Платежи';
                    }
                }
            }
            NEW payments {
                caption = 'Платежи';
                MOVE PROPERTY (nameFilterLegalEntity(c));
                MOVE BOX (pm) { caption = 'Платежи'; }
            }
            MOVE BOX (d) { caption = 'Задолженности'; }
            NEW events {
                caption = 'События';
                fill = 1;
                MOVE BOX (ev);
                NEW add {
                    type = CONTAINERH;
                    fill = 1;
                    NEW description {
                        fill = 1;
                        caption = 'Содержание';
                        MOVE PROPERTY (description(ev)) { caption = ''; fill = 1; }
                    }
                    NEW addition {
                        fill = 1;
                        caption = 'Дополнение';
                        MOVE PROPERTY (addition(ev)) { caption = ''; fill = 1; }
                    }
                }
            }
            NEW projects {
                caption = 'Проекты';
                fill = 1;
                NEW report {
                    caption = 'Отчет по клиенту';
                    type = CONTAINERH;
                    MOVE PROPERTY (dFrom);
                    MOVE PROPERTY (dTo);
                    MOVE PROPERTY (printCustomerReport(c, df, dt));
                }
                NEW projectsTab {
                    type = TABBED;
                    fill = 1;
                    MOVE BOX (pr) { caption = 'Проекты'; }
                    NEW issues {
                        caption = 'Задачи';
                        type = SPLITH;
                        MOVE BOX (st) {
                            fill = 1;
                        }  
                        MOVE BOX (is) { 
                            caption = 'Задачи'; 
                            fill = 6;
                        }
                    }
                    MOVE BOX (te) { caption = 'Отметки времени'; }
                }
            }
            MOVE BOX (ss) { caption = 'Задачи'; }
            MOVE BOX (at) { caption = 'Документы'; }
            NEW info {
                caption = 'Информация';
                fill = 1;
                MOVE PROPERTY (info(c)) { caption = ''; fill = 1; }
            }
        }
    }
}

NAVIGATOR {
    dashboard {
        NEW dashboardCustomer;
    }
}

addLegalEntityCustomer 'Добавить' (Customer c) {
    NESTEDSESSION NEW l = LegalEntity {
        customer(l) <- c;
        IF [GROUP SUM 1 BY customer(LegalEntity e)](c) == 1 THEN FOR customer(Address a) == c DO {
            legalEntity(a) <- l;
        }
        SHOW legalEntity OBJECTS o = l DOCKED;
    }
} IMAGE 'add.png'  CHANGEKEY 'INSERT' HIDE;

EXTEND FORM customer
    OBJECTS le = LegalEntity
    PROPERTIES (le) id, extId, name, fullName, nameLegalAddress, numberBank, nameBank, addressBank, mfoBank, isCompany 
    PROPERTIES DRAW le TOOLBAR addLegalEntityCustomer(o), EDIT(le), DELETE(le)
    FILTERS customer(le) = o
    
    OBJECTS c = Contact.Contact
    PROPERTIES (c) id, firstName, lastName, nameLegalEntity, namePosition, email, phone, addPhones, skype, note, NEW, EDIT, DELETE
    FILTERS customer(c) = o
;
DESIGN customer {
    OBJECTS {
        NEW main FIRST {
            type = CONTAINERH;
            caption = 'Основные параметры';
            MOVE PROPERTY (id(o));
            MOVE PROPERTY (name(o)) { charWidth = 50; }
            MOVE PROPERTY (inactive(o));
        }
        NEW contacts AFTER main {
            type = CONTAINERH;
            caption = 'Контакты';
            MOVE PROPERTY (phone(o));
            MOVE PROPERTY (email(o)) { charWidth = 30; }
        }
        NEW add AFTER contacts {
            type = COLUMNS;
            columns = 3;
            caption = 'Дополнительные параметры';
            MOVE PROPERTY (nameGroup(o));
            MOVE PROPERTY (nameStatus(o));
            MOVE PROPERTY (dateFrom(o));
            MOVE PROPERTY (site(o));
            MOVE PROPERTY (note(o));
        }
        NEW tab {
            type = TABBED;
            fill = 1;
            MOVE BOX (le) { caption = 'Организации'; }
            MOVE BOX (a) { caption = 'Адреса'; }
            MOVE BOX (c) { caption = 'Контакты'; }
            NEW info {
                caption = 'Информация';
                fill = 1;
                MOVE PROPERTY (info(o)) { caption = ''; fill = 1; }
            }
        }
    }
}

NAVIGATOR {
    contractFolder {
        NEW documentFileTypes;
    }
}