MODULE Payment;

REQUIRE LegalEntity, Currency, Finance, AccountLedger;

NAMESPACE Payment;

CLASS Payment 'Платёж' : AccountLedger;
TABLE payment (Payment);

id 'Код' = DATA VARSTRING[15] (Payment) INDEXED CHARWIDTH 5 IN id;
payment (id) = GROUP AGGR Payment c BY id (c);

legalEntity 'Организация' = DATA LegalEntity(Payment) NONULL;
idLegalEntity 'Код организации' (Payment p) = id(legalEntity(p));
nameLegalEntity 'Организация' (Payment p) = name(legalEntity(p)) IN id;
customer (Payment p) = customer(legalEntity(p)) MATERIALIZED INDEXED;
nameCustomer 'Клиент' (Payment p) = name(customer(p));

company = DATA LegalEntity(Payment);
nameCompany 'Компания' (Payment p) = name(company(p)) IN id;

CONSTRAINT company(Payment p) AND NOT isCompany(company(p)) CHECKED BY company[Payment] MESSAGE 'Компания платежа не является компанией';

number 'Номер документа оплаты' = DATA VARSTRING[15](Payment) IN id;
date 'Дата' = DATA DATE (Payment) IN id;
year 'Год' (Payment p) = extractYear(date(p));
monthNumber 'Номер месяца' (Payment p) = extractMonthNumber(date(p));
sum 'Сумма' = DATA NUMERIC[16,5](Payment);
note 'Примечание' = DATA VARISTRING[100](Payment);

currency = DATA Currency(Payment);
nameCurrency 'Валюта платежа' (Payment p) = name(currency(p));
shortNameCurrency 'Валюта (сокр.)' (Payment p) = shortName(currency(p));

sumCurrency 'Сумма в валюте' (Payment p) = round2(sum(p) * OVERRIDE rateOn(defaultTypeExchange(), currency(p), date(p)), 1);

sumPayment 'Сумма платежей' (Customer c) = GROUP SUM sum(Payment p) BY customer(p) MATERIALIZED;

account = DATA Account(Payment);
numberAccount 'Р/с' (Payment p) = number(account(p)) CHARWIDTH 10;

WHEN LOCAL CHANGED (company(Payment p)) AND NOT CHANGED (account(p)) DO {
    account(p) <- account(company(p));
}

CONSTRAINT account(Payment p) AND NOT company(p) == legalEntity(account(p)) CHECKED BY account[Payment] MESSAGE 'Счёт платежа не соответствует компании';

date(Payment p) += date(p);
sum(Payment p) += sum(p);
note(Payment p) += note(p);
currency(Payment p) += currency(p);
account(Payment p) += account(p);

FORM dialogInputSum 'Сумма нового платежа'
    OBJECTS n = NUMERIC[16,5] PANEL
    PROPERTIES sum 'Сумма' = VALUE (n)
;
DESIGN dialogInputSum {
    size = (300,70);
    BOX (n) {
        fill = 1;
        caption = 'Сумма';
        MOVE PROPERTY (sum) { 
            fill = 1;
            caption = ''; 
        }
    }
}

FORM payment 'Платёж'
    OBJECTS o = Payment PANEL
    PROPERTIES(o) id, nameLegalEntity, nameCompany, number, date, sum, shortNameCurrency, sumCurrency, note, numberAccount
        
    EDIT Payment OBJECT o
;

DESIGN payment {
    OBJECTS {
        NEW main FIRST {
            type = CONTAINERH;
            caption = 'Основные параметры';
            MOVE PROPERTY (id(o));
            MOVE PROPERTY (date(o));
            MOVE PROPERTY (number(o));
        }
        NEW contacts AFTER main {
            type = CONTAINERH;
            caption = 'Контакты';
            MOVE PROPERTY (nameLegalEntity(o));
            MOVE PROPERTY (nameCompany(o));
        }
        NEW docs AFTER contacts {
            type = CONTAINERH;
            caption = 'Документы';
        }
        NEW sum AFTER docs {
            type = CONTAINERH;
            caption = 'Сумма';
            MOVE PROPERTY (sum(o));
            MOVE PROPERTY (shortNameCurrency(o));
            MOVE PROPERTY (sumCurrency(o));
            MOVE PROPERTY (numberAccount(o));
        }
        NEW note AFTER sum {
            caption = 'Дополнительные параметры';
            alignment = STRETCH;
            MOVE PROPERTY (note(o));
        }
    }
}

FORM payments 'Платежи'
    OBJECTS o = Payment LAST 
    PROPERTIES(o) READONLY id, nameLegalEntity,nameCustomer, nameCompany, number, date, 
                           year, monthNumber, sum, shortNameCurrency, sumCurrency, note, numberAccount
    ORDER date(o), nameLegalEntity(o), nameCompany(o)
    PROPERTIES(o) NEWSESSION NEW, EDIT, DELETE
    ORDER date(o)
;

FORM dialogPayments 'Платежи'
    OBJECTS o = Payment LAST 
    PROPERTIES(o) READONLY id, nameLegalEntity, nameCustomer, nameCompany, number, date, 
                           year, monthNumber, sum, shortNameCurrency, sumCurrency, note, numberAccount 
    ORDER date(o), nameLegalEntity(o), nameCompany(o)
    
    LIST Payment OBJECT o
;

NAVIGATOR {
    finance {
        NEW payments;
    }
}