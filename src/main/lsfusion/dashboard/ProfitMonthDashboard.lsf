MODULE ProfitMonthDashboard;

REQUIRE PaymentAct, Expense, Dashboard;

NAMESPACE Dashboard;

skipReceipt 'Не учитывать в доходах' = DATA BOOLEAN (LegalEntity);

EXTEND FORM legalEntity PROPERTIES skipReceipt(o);
DESIGN legalEntity {
    main {
        MOVE PROPERTY (skipReceipt(o));
    }   
}

receipt 'Доход' (Month m, INTEGER y) = GROUP SUM sumCurrency(Payment p) IF NOT skipReceipt(legalEntity(p)) BY extractMonth(date(p)), extractYear(date(p));
shipment 'Расход' (Month m, INTEGER y) = GROUP SUM sumCurrency(Expense p) IF NOT skipExpense(type(p)) BY extractMonth(date(p)), extractYear(date(p));
profit 'Прибыль' (Month m, INTEGER y) = receipt(m, y) (-) shipment(m, y);

receipt 'Доход' (Month m, INTEGER y, ContractType t) = GROUP SUM sumCurrency(Payment p) IF NOT skipReceipt(legalEntity(p)) BY extractMonth(date(p)), extractYear(date(p)), type(contract(p));
shipment 'Расход' (Month m, INTEGER y, ExpenseType t) = GROUP SUM sumCurrency(Expense p) IF NOT skipExpense(type(p)) BY extractMonth(date(p)), extractYear(date(p)), type(p);
receiptOther 'Прочее' (Month m, INTEGER y) = GROUP SUM sumCurrency(Payment p) IF NOT type(contract(p)) AND NOT skipReceipt(legalEntity(p)) BY extractMonth(date(p)), extractYear(date(p));
shipmentOther 'Прочее' (Month m, INTEGER y) = GROUP SUM sumCurrency(Expense p) IF NOT type(p) AND NOT skipExpense(type(p)) BY extractMonth(date(p)), extractYear(date(p));
receipt 'Доход' (INTEGER yf, INTEGER yt, ContractType t) = GROUP SUM sumCurrency(Payment p) IF year(p) >= yf AND year(p) <= yt AND NOT skipReceipt(legalEntity(p)) BY type(contract(p));
shipment 'Расход' (INTEGER yf, INTEGER yt, ExpenseType t) = GROUP SUM sumCurrency(Expense p) IF extractYear(date(p)) >= yf AND extractYear(date(p)) <= yt AND NOT skipExpense(type(p)) BY type(p);
receipt 'Доход' (DATE df, DATE dt, ContractType t) = GROUP SUM sumCurrency(Payment p) IF date(p) >= df AND date(p) <= dt AND NOT skipReceipt(legalEntity(p)) BY type(contract(p));
shipment 'Расход' (DATE df, DATE dt, ExpenseType t) = GROUP SUM sumCurrency(Expense p) IF date(p) >= df AND date(p) <= dt AND NOT skipExpense(type(p)) BY type(p);

backgroundReceipt() = RGB (222, 255, 222);
backgroundShipment() = RGB (255, 222, 222);

FORM profitMonthDashboard 'Прибыль по месяцам'
//    OBJECTS dates = (df = DATE, dt = DATE) PANEL 
//    PROPERTIES VALUE (df), VALUE (dt)
    
    OBJECTS years = (yf = INTEGER, yt = INTEGER) PANEL
    PROPERTIES 'Год с' = VALUE (yf), 'Год по' = VALUE (yt)
    
    OBJECTS ct = ContractType
    OBJECTS et = ExpenseType
    OBJECTS my = (m = Month, y = INTEGER)
    PROPERTIES READONLY number(m), 'Месяц' = staticCaption(m), year 'Год' = VALUE (y), receipt(m, y) BACKGROUND backgroundReceipt(), 
                        shipment(m, y) BACKGROUND backgroundShipment(), profit(m, y),
                        receipt(m, y, ct) COLUMNS (ct) HEADER name(ct) BACKGROUND backgroundReceipt(), receiptOther(m, y) BACKGROUND backgroundReceipt(),
                        shipment(m, y, et) COLUMNS (et) HEADER name(et) BACKGROUND backgroundShipment(), shipmentOther(m, y) BACKGROUND backgroundShipment()
    ORDER year, number(m)
    FILTERS y >= yf AND y <= yt, receipt(m, y) OR shipment(m, y), receipt(yf, yt, ct), shipment(yf, yt, et)
    
    OBJECTS p = Payment
    PROPERTIES (p) READONLY id, nameLegalEntity, nameCustomer, numberContract, nameTypeContract, numberAct, number, date, 
                            year, monthNumber, sum, shortNameCurrency, sumCurrency BACKGROUND backgroundReceipt(), note
    FILTERS year(p) == y, extractMonth(date(p)) == m, NOT skipReceipt(legalEntity(p))
    
    OBJECTS e = Expense
    PROPERTIES (e) READONLY nameCompany, nameLegalEntity, nameCustomer, date, datePayment, nameType, sum, shortNameCurrency, 
                            sumCurrency BACKGROUND backgroundShipment(), noteString
    FILTERS extractYear(date(e)) == y, extractMonth(date(e)) == m, NOT skipExpense(type(e))
    
    EVENTS ON INIT {
        SEEK profitMonthDashboard.years OBJECTS yf = extractYear(currentDate()), yt = extractYear(currentDate());
    }
;

DESIGN profitMonthDashboard {
    OBJECTS {
        NEW tab AFTER BOX (my) {
            type = TABBED;
            fill = 1;
            MOVE BOX (p);
            MOVE BOX (e);
        }
    }
}

NAVIGATOR {
    dashboard {
        NEW profitMonthDashboard;
    }
}