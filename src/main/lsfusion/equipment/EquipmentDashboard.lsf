MODULE EquipmentDashboard;

REQUIRE EquipmentLicense, Dashboard, Utils;

NAMESPACE Equipment;

filterContract = DATA LOCAL Contract(SubjectType, Customer);
nameFilterContract 'Договор' (SubjectType t, Customer c) = description(filterContract(t, c));

CONSTRAINT filterContract(SubjectType t, Customer c) AND NOT in(filterContract(t, c), t) CHECKED BY filterContract 
    MESSAGE 'В выбранном договоре нет предметов договора с выбранным типом';
    
CONSTRAINT filterContract(SubjectType t, Customer c) AND NOT customer(filterContract(t, c)) == c CHECKED BY filterContract
    MESSAGE 'Клиент выбранного договора не соответсвует клиенту';
    
addEquipment 'Добавить' (SubjectType t, Customer c) {
    NEWSESSION NESTED (filterContract) NEW e = Equipment {
        subjectType(e) <- t;
        contract(e) <- filterContract(t, c);
        customer(e) <- c;
        SHOW equipment OBJECTS o = e DOCKED;
    }
} IMAGE 'add.png';

quantitySerial 'Кол-во серийных номеров' (Customer c, SubjectType t) = 
    GROUP SUM 1 IF contract(equipment(EquipmentSerial s)) == filterContract(t, c) OR NOT filterContract(t, c) AND c = customer(equipment(s));

FORM equipmentDashboard 'Оборудование'
    OBJECTS t = SubjectType PANEL 
    OBJECTS c = Customer PANEL 
    PROPERTIES name(t) SELECTOR, nameFilterContract(t, c), name(c) SELECTOR, quantitySerial(c, t) READONLY 
    
    OBJECTS e = Equipment
    PROPERTIES (e) READONLY note, quantitySerial, nameCreatedUser, createdDate, createdTime
    PROPERTIES DRAW e TOOLBAR generateLicense(e), downloadFile(e) SHOWIF TRUE IF file(e), addEquipment(t, c) 
    PROPERTIES (e) NEWSESSION EDIT, DELETE SHOWIF TRUE IF NOT file(e)
    FILTERS subjectType(e) == t, customer(e) == c, contract(e) == filterContract(t, c) OR NOT filterContract(t, c)
    
    OBJECTS n = EquipmentSerial
    PROPERTIES (n) READONLY number
    FILTERS equipment(n) == e
;

DESIGN equipmentDashboard {
    OBJECTS {
        NEW head FIRST {
            caption = 'Параметры';
            type = CONTAINERH;
            MOVE PROPERTY (name(t)) { caption = 'Тип предмета договора'; }
            MOVE PROPERTY (name(c)) { caption = 'Клиент'; }
            MOVE PROPERTY (nameFilterContract(t, c)) { caption = 'Договор'; }
            MOVE PROPERTY (quantitySerial(c, t));
        }
        NEW equip {
            type = CONTAINERH;
            fill = 1;
            MOVE BOX (e);
            MOVE BOX (n);
        }
    }
}

NAVIGATOR {
    dashboard {
        NEW equipmentDashboard;
    }
}