MODULE ActTotalDashboard;

REQUIRE Act, Dashboard;

NAMESPACE Act;

filterCompany = DATA LOCAL LegalEntity();
nameFilterCompany 'Организация' () = name(filterCompany());

CONSTRAINT filterCompany() AND NOT isCompany(filterCompany()) CHECKED BY filterCompany[] MESSAGE 'Компания не является компанией';

filterContractType = DATA LOCAL ContractType();
nameFilterContractType 'Тип договора' () = name(filterContractType());

sum 'Сумма по актам' (Month m, INTEGER y, LegalEntity l, ContractType t) = 
    GROUP SUM sum(Act a) IF company(a) == filterCompany() OR NOT filterCompany() BY extractMonth(date(a)), extractYear(date(a)), legalEntity(a), type(contract(a));

sum 'Сумма по актам' (INTEGER y, LegalEntity l, ContractType t) = GROUP SUM sum(Month m, y, l, t);

FORM actTotalDashboard 'Итоги по актам'
    OBJECTS f = (y = INTEGER) PANEL 
    PROPERTIES DRAW f 'Год' = VALUE (y), nameFilterCompany(), nameFilterContractType()
    
    OBJECTS m = Month
    PROPERTIES number(m) SHOWIF NULL 
    ORDERS number(m)
    
    OBJECTS (l = LegalEntity, t = ContractType)
    PROPERTIES READONLY 'Организация' = name(l), 'Тип договора' = name(t), sum(y, l, t) BACKGROUND RGB (255, 255, 222), 
                         sum(m, y, l, t) COLUMNS (m) HEADER staticCaption(m)
    FILTERS sum(y, l, t), t == filterContractType() OR NOT filterContractType()
    ORDERS name(l)
    
    OBJECTS a = Act
    PROPERTIES (a) READONLY id, numberContract, numberAgreement, number, date, fromDate, toDate, name, nameCompany,
                            nameType, sum, nameSendingWay, dateSending, dateReceiving, dateClosing, note, textString
    PROPERTIES (a) NEWSESSION NEW, EDIT, DELETE 
    FILTERS legalEntity(a) == l, type(contract(a)) == t, extractYear(date(a)) == y
    ORDERS date(a)
    
    EVENTS ON INIT {
        SEEK actTotalDashboard.y = currentYear();
    }
;

NAVIGATOR {
    dashboard {
        NEW actTotalDashboard;
    }
}