MODULE DocIssue;

REQUIRE Doc, Issue;

NAMESPACE Doc;

document = DATA Document(Issue);
numberDocument 'Документ' (Issue i) = number(document(i));

CONSTRAINT document(Issue i) AND customer(i) AND NOT customer(document(i)) == customer(i) CHECKED BY document[Issue] MESSAGE 'Клиент задачи не соответствует клиенту документа';

WHEN LOCAL CHANGED (document(Issue i)) AND NOT CHANGED (customer(i)) DO {
    customer(i) <- customer(document(i));
}

EXTEND FORM issues PROPERTIES numberDocument(o);
EXTEND FORM issue PROPERTIES numberDocument(o);

DESIGN issue {
    top {
        MOVE PROPERTY (numberDocument(o));
    }
}

CLASS TaskTemplate 'Шаблон задачи';
TABLE taskTemplate(TaskTemplate);

in 'Вкл.' = DATA BOOLEAN (Type, TaskTemplate);

name 'Наименование' = DATA VARISTRING[100] (TaskTemplate) CHARWIDTH 20;

tracker = DATA Tracker(TaskTemplate);
nameTracker 'Трекер' (TaskTemplate t) = name(tracker(t));

subject 'Тема' = DATA VARISTRING[100](TaskTemplate) CHARWIDTH 20;

assignedTo = DATA Employee(TaskTemplate);
nameAssignedTo 'Назначена' (TaskTemplate t) = name(assignedTo(t));

EXTEND FORM options
    OBJECTS t = Type
    PROPERTIES (t) READONLY nameDocType, name
    ORDER nameDocType(t), name(t)
    
    OBJECTS tt = TaskTemplate
    PROPERTIES in(t, tt)
    PROPERTIES (tt) name, nameTracker, subject, nameAssignedTo, NEW, DELETE 
    FILTERGROUP in FILTER 'Отмеченные' in(t, tt)
    ORDER name(tt)
;

DESIGN options {
    pane {
        NEW issues {
            caption = 'Задачи';
            fill = 1;
            NEW create {
                caption = 'Автоматическое создание задач';
                type = CONTAINERH;
                fill = 1;
                MOVE BOX (t);
                MOVE BOX (tt);
            }
        }
    }
}

WHEN SET (Document d IS Document) AND in(type(d), TaskTemplate t) DO {
    NEW i = Issue {
        tracker(i) <- tracker(t);
        name(i) <- subject(t);
        assignedTo(i) <- assignedTo(t);
        customer(i) <- customer(d);
        description(i) <- text(d);
    }
}