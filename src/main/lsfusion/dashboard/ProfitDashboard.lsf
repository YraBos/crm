MODULE ProfitDashboard;

REQUIRE Dashboard, PaymentAct, TimeEntryExpense;

NAMESPACE Dashboard;

receipt 'Доход' (Customer c, DATE df, DATE dt) = GROUP SUM sum(Payment p) IF date(p) >= df AND date(p) <= dt BY customer(p);
receiptCurrency 'Доход, USD' (Customer c, DATE df, DATE dt) = round2(receipt(c, df, dt) * rateOn(defaultTypeExchange(), defaultCurrency(), currentDate()));
shipmentCurrency 'Расход, USD' (Customer c, DATE df, DATE dt) = round2(costSum(c, df, dt) * rateOn(defaultTypeExchange(), defaultCurrency(), currentDate()));
diff 'Прибыль, USD' (Customer c, DATE df, DATE dt) = receiptCurrency(c, df, dt) (-) shipmentCurrency(c, df, dt);
diffPercent 'Прибыль, %' (Customer c, DATE df, DATE dt) = round2(diff(c, df, dt) / receiptCurrency(c, df, dt) * 100);
shipmentCurrency 'Расход, USD' (Employee e, Customer c, DATE df, DATE dt) = round2(costSum(e, c, df, dt) * rateOn(defaultTypeExchange(), defaultCurrency(), currentDate()));
shipmentCurrency 'Расход, USD' (Project p, Customer c, DATE df, DATE dt) = round2(costSum(p, c, df, dt) * rateOn(defaultTypeExchange(), defaultCurrency(), currentDate()));
shipmentCurrency 'Расход, USD' (Project p, Employee e, Customer c, DATE df, DATE dt) = round2(costSum(p, e, c, df, dt) * rateOn(defaultTypeExchange(), defaultCurrency(), currentDate()));

FORM profit 'Прибыль по клиентам'
    OBJECTS period = (df = DATE, dt = DATE) PANEL
    PROPERTIES 'Дата с' = VALUE (df), 'Дата по' = VALUE (dt)
    
    OBJECTS c = Customer
    PROPERTIES READONLY name(c), receiptCurrency(c, df, dt), shipmentCurrency(c, df, dt), diff(c, df, dt), diffPercent(c, df, dt)
    FILTERS costSum(c, df, dt) OR receipt(c, df, dt)
    
    OBJECTS p = Payment
    PROPERTIES (p) READONLY id, nameLegalEntity, nameCustomer, numberContract, nameTypeContract, numberAct, number, date, 
                                                       year, monthNumber, sum, nameCurrency, shortNameCurrency, sumCurrency, note
    
    FILTERS customer(p) == c
    FILTERGROUP paymentInterval
        FILTER 'За интервал' date(p) >= df AND date(p) <= dt DEFAULT
    
    OBJECTS e = Employee
    PROPERTIES READONLY name(e)
    PROPERTIES (e, c, df, dt) READONLY hours, costSum, shipmentCurrency
    FILTERS hours(e, c, df, dt)
    
    OBJECTS t = TimeEntry
    PROPERTIES (t) READONLY id, hours, commentsString, date, nameActivity, nameProject, nameEmployee, numberIssue, nameIssue
    FILTERS employee(t) == e, date(t) >= df AND date(t) <= dt, customer(t) == c
    
    OBJECTS pp = Project
    PROPERTIES READONLY name(pp)
    PROPERTIES (pp, c, df, dt) READONLY hours, costSum, shipmentCurrency
    FILTERS hours(pp, c, df, dt)
    
    OBJECTS ee = Employee
    PROPERTIES READONLY name(ee)
    PROPERTIES (pp, ee, c, df, dt) READONLY hours, costSum, shipmentCurrency
    FILTERS hours(pp, ee, c, df, dt)
    
    OBJECTS tt = TimeEntry
    PROPERTIES (tt) READONLY id, hours, commentsString, date, nameActivity, numberIssue, nameIssue
    FILTERS employee(tt) == ee, date(tt) >= df AND date(tt) <= dt, customer(tt) == c, project(tt) == pp
    FILTERGROUP active FILTER 'Активные' NOT inactive(c) DEFAULT 
;

DESIGN profit {
    OBJECTS {
        NEW tab AFTER BOX (c){
            type = TABBED;
            fill = 1;
            MOVE BOX (p) { caption = 'Платежи'; }
            NEW hours {
                type = TABBED;
                fill = 1;
                caption = 'Трудозатраты';
                NEW employee {
                    caption = 'По сотрудникам';
                    type = CONTAINERH;
                    MOVE BOX (e) { fill = 1; }
                    MOVE BOX (t) { fill = 5; }
                }
                NEW project {
                    caption = 'По проектам';
                    type = CONTAINERH;
                    MOVE BOX (pp) { fill = 1; }
                    MOVE BOX (ee) { fill = 1; }
                    MOVE BOX (tt) { fill = 4; }
                }
            }
        }
    }
}

NAVIGATOR {
    dashboard {
        NEW profit;
    }
}