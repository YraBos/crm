MODULE Payment;

REQUIRE Act, Currency, Finance;

NAMESPACE Payment;

CLASS Payment 'Платёж';
TABLE payment (Payment);

id 'Код' = DATA VARSTRING[15] (Payment) INDEXED CHARWIDTH 5 IN id;
payment (id) = GROUP AGGR Payment c BY id (c);

legalEntity 'Организация' = DATA LegalEntity(Payment) NONULL;
idLegalEntity 'Код организации' (Payment p) = id(legalEntity(p));
nameLegalEntity 'Организация' (Payment p) = name(legalEntity(p)) IN id;
customer (Payment p) = customer(legalEntity(p));
nameCustomer 'Клиент' (Payment p) = name(customer(p));

company = DATA LegalEntity(Payment);
nameCompany 'Компания' (Payment p) = name(company(p)) IN id;

contract = DATA Contract(Payment);
idContract 'Код договора' (Payment a) = id(contract(a));
numberContract 'Номер договора' (Payment a) = number(contract(a)) IN id;
typeContract (Payment p) = type(contract(p));
nameTypeContract 'Тип договора' (Payment p) = nameType(contract(p));

act = DATA Act(Payment);
idAct 'Код акта' (Payment a) = id(act(a));
numberAct 'Номер акта' (Payment a) = number(act(a)) IN id;

agreement = DATA Agreement(Payment);
numberAgreement 'Номер соглашения' (Payment p) = number(agreement(p));

number 'Номер документа оплаты' = DATA VARSTRING[15](Payment) IN id;
date 'Дата' = DATA DATE (Payment) IN id;
year 'Год' (Payment p) = extractYear(date(p));
monthNumber 'Номер месяца' (Payment p) = extractMonthNumber(date(p));
sum 'Сумма' = DATA NUMERIC[16,5](Payment);
note 'Примечание' = DATA VARSTRING[100](Payment);

currency = DATA Currency(Payment);
nameCurrency 'Валюта платежа' (Payment p) = name(currency(p));
shortNameCurrency 'Валюта (сокр.)' (Payment p) = shortName(currency(p));

sumCurrency 'Сумма в валюте' (Payment p) = round2(sum(p) * OVERRIDE rateOn(defaultTypeExchange(), currency(p), date(p)), 1);

paid 'Оплачено' (Contract c) = GROUP SUM sum(Payment p) BY contract(p);
paid 'Оплачено' (Act c) = GROUP SUM sum(Payment p) BY act(p);

CONSTRAINT company(Payment p) AND NOT isCompany(company(p)) CHECKED BY company[Payment] MESSAGE 'Компания платежа не является компанией';
CONSTRAINT legalEntity(Payment p) != legalEntity(contract(p)) CHECKED BY contract[Payment] MESSAGE 'Организация платежа должна совпадать с организацией договора';
CONSTRAINT company(Payment p) != company(contract(p)) CHECKED BY contract[Payment] MESSAGE 'Компания платежа должна совпадать с компанией договора';
CONSTRAINT contract(agreement(Payment p)) != contract(p) CHECKED BY agreement[Payment] MESSAGE 'Договор соглашения должен совпадать с договором платежа';
CONSTRAINT contract(act(Payment p)) != contract(p) CHECKED BY act[Payment] MESSAGE 'Договор акта должен совпадать с договором платежа';

FORM dialogInputSum 'Сумма нового платежа'
    OBJECTS n = NUMERIC[16,5] PANEL
    PROPERTIES sum 'Сумма' = VALUE (n)
;
DESIGN dialogInputSum {
    size = (300,70);
    BOX (n) {
        fill = 1;
        caption = 'Сумма';
        MOVE PROPERTY (sum) { 
            fill = 1;
            caption = ''; 
        }
    }
}

split 'Разбить платеж' (Payment p) {
    NEWSESSION DIALOG dialogInputSum OBJECTS n INPUT DO {
        IF n < sum(p) THEN NEW pp = Payment {
            sum(p) <- sum(p) (-) n;
            sum(pp) <- n;
            legalEntity(pp) <- legalEntity(p);
            contract(pp) <- contract(p);
            act(pp) <- act(p);
            number(pp) <- number(p);
            date(pp) <- date(p);
            note(pp) <- note(p);
            currency(pp) <- currency(p);
            APPLY;
        }
        ELSE MESSAGE 'Сумма больше суммы платежа';
    }
}

FORM payment 'Платёж'
    OBJECTS o = Payment PANEL
    PROPERTIES(o) id, nameLegalEntity, nameCompany, numberContract, numberAgreement, numberAct, number, date, sum, shortNameCurrency, sumCurrency, note
        
    EDIT Payment OBJECT o
;

DESIGN payment {
    OBJECTS {
        NEW main FIRST {
            type = CONTAINERH;
            caption = 'Основные параметры';
            MOVE PROPERTY (id(o));
            MOVE PROPERTY (date(o));
            MOVE PROPERTY (number(o));
        }
        NEW contacts AFTER main {
            type = CONTAINERH;
            caption = 'Контакты';
            MOVE PROPERTY (nameLegalEntity(o));
            MOVE PROPERTY (nameCompany(o));
        }
        NEW docs AFTER contacts {
            type = CONTAINERH;
            caption = 'Документы';
            MOVE PROPERTY (numberContract(o));
            MOVE PROPERTY (numberAgreement(o));
            MOVE PROPERTY (numberAct(o));
        }
        NEW sum AFTER docs {
            type = CONTAINERH;
            caption = 'Сумма';
            MOVE PROPERTY (sum(o));
            MOVE PROPERTY (shortNameCurrency(o));
            MOVE PROPERTY (sumCurrency(o));
        }
        NEW note AFTER sum {
            caption = 'Дополнительные параметры';
            alignment = STRETCH;
            MOVE PROPERTY (note(o));
        }
    }
}

FORM payments 'Платежи'
    OBJECTS o = Payment LAST 
    PROPERTIES(o) READONLY id, nameLegalEntity,nameCustomer, nameCompany, numberContract, nameTypeContract, numberAgreement, numberAct, number, date, 
                           year, monthNumber, sum, shortNameCurrency, sumCurrency, note    
    ORDER date(o), nameLegalEntity(o), nameCompany(o), numberContract(o)
    PROPERTIES(o) NEWSESSION NEW, EDIT, DELETE
    ORDER date(o)
;

FORM dialogPayments 'Платежи'
    OBJECTS o = Payment LAST 
    PROPERTIES(o) READONLY id, nameLegalEntity, nameCustomer, nameCompany, numberContract, nameTypeContract, numberAgreement, numberAct, number, date, 
                           year, monthNumber, sum, shortNameCurrency, sumCurrency, note    
    ORDER date(o), nameLegalEntity(o), nameCompany(o), numberContract(o)
    
    LIST Payment OBJECT o
;

NAVIGATOR {
    finance {
        NEW payments;
    }
}