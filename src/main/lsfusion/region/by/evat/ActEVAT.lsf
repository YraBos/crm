MODULE ActEVAT;

REQUIRE Act, EVAT;

NAMESPACE Act;

lastEVATAct 'Последний ЭСЧФ по акту' = GROUP LAST EVAT e ORDER date(e), e WHERE NOT cancelled(e) BY EVATDocument(e) MATERIALIZED ;

// запрет корр-ки акта, если у него есть ЭСЧФ
forbidChange(Act a) += TRUE IF lastEVATAct(a) AND a IS Act;

in 'Вкл.' = DATA LOCAL BOOLEAN (Act);

viewEVATActDateMore 'Начальная дата отображения актов: ' = DATA LOCAL DATE () ;

isEVAT 'Формировать ЭСЧФ' = DATA BOOLEAN (LegalEntity);
EXTEND FORM legalEntity PROPERTIES(o) isEVAT;
EXTEND FORM legalEntities PROPERTIES(o) READONLY isEVAT;

DESIGN legalEntity {
    bank{
        MOVE PROPERTY (UNP(o));
        MOVE PROPERTY (OKPO(o));
        MOVE PROPERTY (isEVAT(o));
    }
}

generateEVAT 'Сформировать ЭСЧФ' ()  {
    
    LOCAL count = INTEGER ();
    count() <- 0;
    
    FOR in(Act a) DO NEWSESSION NESTED LOCAL {
        NEW e = EVAT {
            EVATDocument(e) <- a;
            generateSeriesNumber(e);
            unpSender(e) <- '101262078';
            date(e) <- currentDate();
            customer(e) <- legalEntity(contract(a));
            supplier(e) <- company(contract(a));
            dataDateDoc(e) <- date(a);
            numberContract(e) <- numberContract(a);
            dateContract(e) <- dateContract(a);
            numberDoc(e) <- number(a);
            
            NEW d = EVATDetail {
                EVAT(d) <- e;
                number(d) <- 1; 
                quantity(d) <- 0;
                price(d) <- 0;
                sum(d) <- sum(a);
                exciseSum(d) <- 0;
                vatRate(d) <- 0;
                vatRateType(d) <- 'NO_VAT';
                vatSum(d) <- 0;
                sumWithVAT(d) <- sum(d) + vatSum(d);
                codeOced(d) <- '62010';
                code(d) <- '';
                name(d) <- IF nameType(a)=='АСР' THEN 'Услуги по эксплуатации ПО в период с ' + 
                            OVERRIDE toChar(fromDate(a),'DD.MM.YYYY'),'?' + ' по ' + OVERRIDE toChar(toDate(a),'DD.MM.YYYY'),'?' 
                           ELSE textString(a) ;
                descriptionType(d) <- DescriptionType.vatExcemption;
            }
        }
        in(a) <- NULL;
        count() <- count() + 1;
        APPLY NESTED LOCAL;
    }
    MESSAGE CONCAT ' ', 'Количество созданных ЭСЧФ: ', count() ;
}


FORM ActEVATs 'Формирование электронных счет-фактур'
    
    OBJECTS date = DATE PANEL
    PROPERTIES value = VALUE(date)
    
    OBJECTS a = Act
    PROPERTIES(a) in
    PROPERTIES(a) READONLY nameCompany, number, date, dateSending, dateReceiving, name, nameType, numberContract, numberAgreement, nameLegalEntity, sum, textString SELECTOR
    PROPERTIES() generateEVAT DRAW a TOOLBAR
    FILTERS NOT lastEVATAct(a) AND isEVAT(company(a)) AND (NOT date OR date(a) >= date)
    
    OBJECTS dates = (dFrom = DATE, dTo = DATE) PANEL
    PROPERTIES dFrom = VALUE(dFrom), dTo = VALUE(dTo)
    
    OBJECTS e = EVAT
    PROPERTIES(e) READONLYIF isReadonly() exported, cancelled, unpSender, number, exportYear, exportNumber, dateDoc, numberDoc, seriesDoc,
                  nameSupplier, nameCustomer, totalSum,
                  totalExciseSum, totalVATSum, totalSumWithVAT
    
    FILTERS date(e) >= dFrom, date(e) <= dTo
    
    FILTERGROUP exported FILTER 'Выгруженные' exported(e) 
                         FILTER 'Не выгруженные' noexported(e) 
    FILTERGROUP cancelled FILTER 'Отмененные' cancelled(e) 
                          FILTER 'Не отмененные' nocancelled(e) DEFAULT 

    EVENTS
        ON INIT { SEEK ActEVATs.date = DATE(TEXT(currentYear())+'01'+'01'); }
;

DESIGN ActEVATs {
    OBJECTS{

        MOVE BOX(date) BEFORE BOX(e) { 
            caption = '';
            type = CONTAINERH;
            PROPERTY(value){caption = 'Дата начала отображения актов';} 
        }
        NEW acts BEFORE BOX(e){
            type = TABBED;
            fill = 1;
            
            MOVE BOX(a) {
                MOVE PROPERTY(generateEVAT());
            }
        }
        NEW filters BEFORE BOX(e) {
            MOVE BOX(dates) {
                GROUP(,dates) {
                    type = CONTAINERH;
                    PROPERTY(dFrom) {
                        caption = 'Дата с';
                    }
                    PROPERTY(dTo) {
                        caption = 'Дата по';
                    }
                }
            }
        }
    }
}

NAVIGATOR {
    EVATFolder {
        NEW ActEVATs;
    }
}

